---
tags:
  - 1С
---
## Теория

Для получения значения ссылок на предопределенные элементы справочников, планов видов характеристик, планов счетов, планов видов расчета, ссылки на значения перечислений, системных перечислений и точек маршрута бизнес-процессов в коде, выполняемом в клиентском коде (тонкий клиент, веб-клиент), где объекты типа **`СправочникМенеджер`.`<ИмяСправочника>`**, **`ПеречислениеМенеджер`.`<ИмяПеречисления>`** и т.п. не доступны, предназначена функция глобального контекста **`ПредопределенноеЗначение`**.  
Например:

```js
ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо");
```

При использовании в конфигурации Библиотеки стандартных подсистем (БСП) версии 2.1.4 и выше рекомендуется использовать функцию **`ПредопределенныйЭлемент`** общего модуля **`ОбщегоНазначения`** или **`ОбщегоНазначенияКлиент`**, которая возвращает **`Неопределено`** для несуществующих в ИБ предопределенных элементов. При этом не следует реализовывать дополнительные механизмы кеширования на клиенте предопределенных значений. Указанные выше функции не ухудшают [клиент-серверное взаимодействие](https://its.1c.ru/db/v8std/content/487/hdoc): серверный вызов выполняется только при первом обращении к значению, а результат автоматически кэшируется.

---
## Задача

Как уже не раз говорилось, работа с данными прикладных объектов на клиенте невозможна. Однако с помощью метода глобального контекста `ПредопределенноеЗначение()` на клиенте можно получать значения ссылок на предопределенные элементы справочников, планов видов характеристик, ссылки на значения перечислений и т.п. 

Поэтому если нужно выполнить какое-то действие на клиенте, например открыть форму и передать в качестве параметра при ее открытии какое-то предопределенное значение, то можно получить его тут же, на клиенте, не обращаясь для этого к серверу. 

Но если не задумываться о минимизации серверных вызовов с клиента и делать все «по старинке», то можно получить это предопределенное значение с сервера непосредственно перед открытием параметризированной формы.

Посмотрим, какое решение будет наиболее эффективным. 

Предположим, в конфигурации существует документ `ОказаниеУслуги`, содержащий реквизит `ВидРаботы`. При заполнении соответствующего поля документа указывается, где выполнялась услуга: на дому или в офисе. Выбор нужного значения тумблера выполняется из значений перечисления `ВидыРабот` (`ВОфисе`, `НаДому`) – рис. 4.55.

![[Pasted image 20240803154739.png]]

При нажатии кнопки `Список услуг на дому` из списка документов `ОказаниеУслуги` должен открываться отчет `РеестрОказанныхУслуг`, выводящий список документов об оказании услуг, выполненных на дому. То есть при формировании отчета к списку услуг должен быть применен отбор по виду работы. И при этом отчет должен быть сформирован сразу же при его открытии (рис. 4.56).

![[Pasted image 20240803154854.png]]
## Первый вариант решения

Создадим форму списка документа `ОказаниеУслуги` и ее команду `СписокУслугНаДому`. Перетащим команду в командную панель формы. Обработчик команды заполним следующим образом:

```js
&НаКлиенте 
Процедура СписокУслугНаДому(Команда) 

	ОтборПоВидуРаботы = Новый Структура("ВидРаботы", ПолучитьВидРаботы()); 
	ПараметрыФормы = Новый Структура("Отбор, СформироватьПриОткрытии", 
		ОтборПоВидуРаботы, Истина); 
	ОткрытьФорму("Отчет.РеестрОказанныхУслуг.Форма", ПараметрыФормы); 
	
КонецПроцедуры
```

В этом обработчике мы создаем структуру `ОтборПоВидуРаботы`, которая задает условие отбора – по виду работ, которые оказывались на дому. Для того чтобы получить ссылку на значение `НаДому` перечисления `ВидыРабот`, мы используем функцию `ПолучитьВидРаботы()`, которая выполняется на сервере без контекста формы. 

Затем формируем структуру параметров формы (`ПараметрыФормы`), описав в ней значения двух стандартных параметров, поставляемых расширением отчета: `Отбор` и `СформироватьПриОткрытии`. Последний параметр устанавливаем в `Истина`, чтобы отчет формировался сразу же при его открытии. Этот вопрос мы подробно рассматривали в предыдущем примере, поэтому не будем еще раз на нем останавливаться. 

И затем открываем форму отчета, передав в нее указанные параметры. 

Для получения ссылки на значение перечисления `ВидыРабот` поместим в модуле формы функцию `ПолучитьВидРаботы()`, исполняющуюся на сервере без контекста формы:

```js
&НаСервереБезКонтекста 
Функция ПолучитьВидРаботы() 
	Возврат Перечисления.ВидыРабот.НаДому; 
КонецФункции
```

Запустим «1С:Предприятие», откроем список документов `Оказание услуг` и нажмем кнопку `Список услуг на дому` (рис. 4.56). В результате будет открыт и сразу же сформирован отчет `РеестрОказанныхУслуг`, выводящий список документов об оказании услуг, выполненных на дому. 

Вроде бы мы все сделали правильно – открыли форму отчета с параметрами (как и было рекомендовано в предыдущих примерах), но все равно при этом были сделаны два обращения на сервер (рис. 4.57)

![[Pasted image 20240803155934.png|500]]

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера 
(рис. 4.58).

![[Pasted image 20240803155957.png]]

Второй вызов сервера происходит при открытии формы отчета, и его делает сама платформа. 

А вот первого вызова, который происходит при получении ссылки на значение перечисления, можно избежать. Ниже мы покажем, как это сделать.
## Второй вариант решения

На самом деле для получения ссылки на значение перечисления вовсе не обязательно обращаться на сервер. Это можно сделать на клиенте с помощью метода `ПредопределенноеЗначение()`. И затем уже передавать полученное значение в форму отчета при ее открытии. 

Итак, изменим обработчик команды `СписокУслугНаДому` следующим образом:

```js
&НаКлиенте 
Процедура СписокУслугНаДому(Команда) 

	ВидРаботы = ПредопределенноеЗначение("Перечисление.ВидыРабот.НаДому"); 
	ОтборПоВидуРаботы = Новый Структура("ВидРаботы", ВидРаботы); 
	ПараметрыФормы = Новый Структура("Отбор, СформироватьПриОткрытии", 
		ОтборПоВидуРаботы, Истина); 
	ОткрытьФорму("Отчет.РеестрОказанныхУслуг.Форма", ПараметрыФормы); 
	
КонецПроцедуры
```

А функцию `ПолучитьВидРаботы()` удалим из модуля формы. 

Запустим «1С:Предприятие», откроем список документов `Оказание услуг` и нажмем кнопку `Список услуг на дому` (рис. 4.56). В результате будет открыт и сразу же сформирован отчет `РеестрОказанныхУслуг`, выводящий список документов об оказании услуг, выполненных на дому. 

Поскольку ссылку на значение перечисления `ВидыРабот` мы получаем на клиенте, нам не понадобится лишний раз обращаться на сервер.

В результате та же функциональность будет реализована за один серверный вызов, и производительность прикладного решения будет выше (рис. 4.59).

![[Pasted image 20240803160256.png|500]]

Кроме того, можно открывать форму отчета `РеестрОказанныхУслуг` без параметров, а значения параметров `Отбор` и `СформироватьПриОткрытии` устанавливать в обработчике события формы `ПриСозданииНаСервере`. Но для этого, во-первых, нужно создавать форму отчета, а во-вторых, проверять в этом обработчике значение параметра `Отбор`. Так как, в общем случае, отчет может быть открыт без отбора.
## Резюме

В целях минимизации серверных вызовов в тех случаях, когда на клиенте требуется получать значения ссылок на предопределенные элементы справочников, планов видов характеристик, ссылки на значения перечислений и т.п., можно сделать это с помощью метода глобального контекста `ПредопределенноеЗначение()`, не обращаясь лишний раз к серверу. При этом клиент-серверное взаимодействие не ухудшается, так как серверный вызов выполняется только при первом обращении к значению, а результат автоматически кэшируется в кэше конфигурации на клиенте до следующего обновления версии конфигурации или версии платформы.

Поэтому *при выполнении каких-то действий на клиенте не нужно вызывать серверные функции для получения ссылок на предопределенные значения, значения перечислений и* 
*т. п. Это потребует лишнего обращения к серверу и будет неэффективно. Вместо этого лучше использовать метод глобального контекста `ПредопределенноеЗначение()`.* 

Таким образом, из показанных вариантов решения задачи более эффективным будет 
второй – за счет получения ссылки на значение перечисления *на клиенте без обращения к серверу*.