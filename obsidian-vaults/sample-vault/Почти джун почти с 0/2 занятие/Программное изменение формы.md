---
tags:
  - 1С
---
## Теория

Форма не рождается на клиенте. Форма рождается на сервере, она проходит несколько важных стадий предварительной обработки, прежде чем достичь глаз пользователя. Платформа содержит достаточно сложные механизмы многоуровневого кеширования различных частей формы как на сервере, так и на клиенте.

По этой причине возможность программного изменения формы является скорее опциональной, дополнительной. Она рассчитана на отдельные конкретные сценарии работы и не предполагает массового использования в большом количестве форм конфигурации или в часто используемых формах. Основным подходом при разработке прикладных решений должно являться визуальное конструирование форм в конфигураторе. А программное изменение форм рекомендуется использовать лишь в отдельных специфических сценариях работы.

Такими сценариями могут быть, например, отображение в форме имеющихся типовых операций или характеристик объектов. То есть той информации, которая содержится в базе данных и структура которой неизвестна на этапе конфигурирования. Ее можно узнать только уже в процессе функционирования прикладного решения, в режиме _1С:Предприятие_. Поэтому для ее отображения в форме и требуется ее программное изменение.

Встроенный язык позволяет добавлять, изменять и удалять реквизиты, команды и элементы формы. Особенность заключается в том, что программно удалить можно только то, что программно же и добавлено. _Нельзя программно удалить элементы, реквизиты или команды, созданные в конфигураторе._

При программном изменении формы нужно управлять всей «троицей»: _реквизиты_, _команды_ и _элементы_. Например, чтобы разместить какие-то данные в форме, нужно создать реквизит, создать элемент, связать элемент с реквизитом. Для команды – создать команду, связать ее с имеющимся в модуле формы обработчиком, создать элемент, связать элемент с командой.

Добавление и удаление реквизитов, команд и элементов формы возможно только на сервере. Поэтому алгоритмы модификации формы нужно размещать в серверных процедурах формы – в обработчиках событий формы на сервере или в контекстных серверных процедурах модуля формы.

Программное изменение формы возможно только «изнутри» формы, т. е. при нахождении в ее модуле. Нет возможности получить какую-либо форму, программно изменить ее и затем открыть эту форму. Потому что изменение формы возможно только на сервере, а открытие формы – только на клиенте. А встроенный язык не содержит методов, позволяющих разработчику принудительно передать форму с сервера на клиент.

Механика добавления/удаления реквизитов формы принципиально отличается от механики работы с ее элементами и командами.

Элементы и команды можно добавлять/удалять поодиночке, просто обращаясь к коллекциям этих элементов, используя методы _`Добавить()`_ и _`Удалить()`_. Это относительно «безболезненные» операции для формы.

Изменение состава реквизитов, напротив, является сложной и затратной операцией. Поэтому здесь используется следующий подход. Сначала разработчик создает два массива программных объектов, которые описывают реквизиты формы. Один массив – это те реквизиты, которые должны быть добавлены, а другой массив – это те реквизиты, которые нужно удалить. После этого «за один подход» выполняется модификация формы с помощью ее метода _`ИзменитьРеквизиты()`_, в который передаются оба этих массива. Сначала выполняется удаление реквизитов, затем – добавление.

С добавлением реквизитов связана еще одна важная особенность. Можно добавить реквизит и установить его свойства. Но из встроенного языка нельзя назначить реквизит основным. Поэтому, например, полностью программно невозможно создать «настоящую» форму списка или объекта.
## Примеры

Теперь в качестве примеров рассмотрим четыре случая программного изменения формы:
- добавление поля,
- добавление команды.
### Добавление поля

Разберём пример добавление поля на примере «АдресКартинки». Мы уже добавили это поле в конфигураторе, а теперь попробуем сделать это же из встроенного языка.

Для изменения формы сразу же выполним контекстный вызов сервера, в котором и будем модифицировать форму.

```js
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДобавитьПолеНаСервере();
	
КонецПроцедуры
```

```js
&НаСервере
Процедура ДобавитьПолеНаСервере()
	
	// Добавить реквизит
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Строка"));
	
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
	
	НовыйРеквизит = Новый РеквизитФормы("АдресКартинки",  // имя
		ОписаниеТиповДляРеквизита,		  				  // тип
		,                                                 // путь
		"Адрес картинки",                            // заголовок
		Истина);                            // сохраняемые данные
		
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

```

В этом фрагменте мы сначала создаем объект, описывающий тип добавляемого реквизита. Поскольку в общем случае реквизит может иметь составной тип, т. е. включать в себя сразу несколько типов, то для описания его типа используется объект _`ОписаниеТипов`_. Этот объект создается на основе массива, содержащего нужные типы.

Наш реквизит будет иметь тип _`Строка`_, поэтому мы создаем массив _ТипыРеквизита_, содержащий единственный элемент. Затем на основании этого массива создаем описание типов – _`ОписаниеТиповДляРеквизита`_. Это описание типов мы используем при конструировании реквизита формы – _`НовыйРеквизитФормы(…)`_.

В параметрах конструктора мы указываем:
- Имя реквизита – _`АдресКартинки`_.
- Тип этого реквизита – созданное нами описание типов _`ОписаниеТиповДляРеквизита`_.
- Путь к реквизиту не указываем, поскольку добавляем реквизит первого уровня.
- Заголовок реквизита – _`Адрес картинки`_, этот заголовок будет использоваться платформой для отображения в форме.
- Последним параметром указываем, что реквизит содержит сохраняемые данные, т. е. при изменении этого реквизита будет автоматически устанавливаться модифицированность формы. В данном случае этот параметр мы используем исключительно в демонстрационных целях, чтобы показать, что такая возможность имеется. Благодаря этому в дальнейшем перед закрытием формы мы сможем анализировать ее модифицированность и предотвращать закрытие формы без сохранения измененных данных.

После того как реквизит создан, мы добавляем его в массив и этот массив передаем в метод формы _`ИзменитьРеквизиты()`_ первым параметром. То есть как те реквизиты, которые нужно добавить в форму.

Теперь мы имеем состояние, будто мы интерактивно в конфигураторе добавили реквизит:

![[Pasted image 20240724194841.png]]

На втором этапе, как мы говорили выше, нужно создать элемент формы. Для этого мы используем следующий код:

```js
// добавить элемент формы и связать его с реквизитом
НовыйЭлемент = Элементы.Добавить("АдресКартинки", Тип("ПолеФормы"), Элементы.ГруппаКартинка);

НовыйЭлемент.ПутьКДанным = "АдресКартинки";
НовыйЭлемент.Вид = ВидПоляФормы.ПолеКартинки;
```

После того как элемент добавлен и связан с реквизитом, установим некоторые его свойства. Во-первых, укажем, что это не просто поле, а поле картинки.

В коде обращаться к такому добавленному полю формы можно через:
```js
ЭтотОбъект.АдресКартинки;
```

После этого можно запустить систему в режиме _1С:Предприятие_ и посмотреть, как работает наша команда _`Добавить поле`_.
### Добавление команды

После того, как мы проверили, что наш код работает, добавим команду.

Добавление команды аналогично добавлению реквизита, но немного проще.

Сначала добавим команду _`ИзменитьКартинку`._

```js
// Добавление команды
НоваяКоманда = Команды.Добавить("ИзменитьКартинку");
НоваяКоманда.Действие = "ИзменитьКартинку";// Процедура на клиенте
НоваяКоманда.Картинка = БиблиотекаКартинок.Изменить;
НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
НоваяКоманда.Заголовок = "Изменить картинку";
	
// Добавить кнопку и связать её с командой
НовыйЭлемент = Элементы.Добавить("ИзменитьКартинку", Тип("КнопкаФормы"), Элементы.ГруппаКартинка);
НовыйЭлемент.ИмяКоманды = "Изменить картинку";
```

Далее, назначим этой команде обработчик – процедуру, которая была создана в модуле формы заранее, на этапе конфигурирования «ИзменитьКартинку».

После создадим элемент для команды и свяжем её с командой, которую мы только что создали.

После этого, мы можем запустить систему в в режиме _1С:Предприятие_ и посмотреть, как работает наша команда.