---
tags:
  - 1С
---
1. Если в модулях конфигурации встречаются строки, предназначенные для пользовательского интерфейса (сообщения пользователю, надписи в формах, названия и подсказки команд, выражения в настройках СКДи т.п.) необходимо обеспечить возможность локализации таких строк.

Для этого необходимо применять функцию **[[НСтр() - новая строка|НСтр]]** вместо прямого использования строковых литералов. Иное использование строк, предназначенных для пользовательского интерфейса, не допускается.

Например, неправильно:

```js
ПоказатьПредупреждение(, "Для выполнения операции необходимо установить расширение работы с файлами.");
```

Правильно:

```js
ПоказатьПредупреждение(, НСтр("ru='Для выполнения операции необходимо установить расширение работы с файлами.'"));
```

Также следует обращать внимание на корректное использование функции **[[НСтр() - новая строка|НСтр]]**.

Например, неправильно:

```js
ТекстСообщения = "ru='Для выполнения операции необходимо установить расширение работы с файлами.'";
ПоказатьПредупреждение(, НСтр(ТекстСообщения));
```

Правильно:

```js
ТекстСообщения = НСтр("ru='Для выполнения операции необходимо установить расширение работы с файлами.'");
ПоказатьПредупреждение(, ТекстСообщения);
```

2. В том случае если строка является составной и включает в себя части, зависящие от тех или иных условий, тем не менее, следует использовать логически завершенные, целостные фразы (предложения). Необходимо применять функцию **`СтрШаблон`** (или аналогичную) для подстановки параметров в строки сообщений пользователю.

Это требование обусловлено, во-первых, разным расположением параметров в тексте предложения на различных языках, что приводит к необходимости изменения исходного кода для перестановки складываемых фрагментов строк, а во-вторых, невозможностью корректно перевести отдельные несогласованные части предложения (наличие или отсутствие артиклей, предлогов, разное расположение знаков препинания в тексте предложения на различных языках и т.п.).

Неправильно:

```js
СообщениеОНехватке = "Не хватает товара " + НаименованиеТовара + " на складе " + НаименованиеСклада + ".";
```
Правильно:

```js
ТекстСообщения = НСтр("ru = 'Не хватает товара %1 на складе %2.'");
СообщениеОНехватке = СтрШаблон(ТекстСообщения, НаименованиеТовара, НаименованиеСклада);
```

Также с помощью параметров подстановки не следует разрывать целостную фразу на отдельные логически незавершенные части. Вместо этого следует задавать в коде несколько строк со всеми вариантами фразы.

Неправильно:

```js
НСтр("ru = '%1 пользователя ""%2"" в группу ""%3""?'")
// где параметр %1 может принимать слова "Включить", "Копировать" или "Удалить".
```

Правильно:

```js
НСтр("ru = 'Включить пользователя ""%2"" в группу ""%3""?'")
НСтр("ru = 'Копировать пользователя ""%2"" в группу ""%3""?'")
НСтр("ru = 'Удалить пользователя ""%2"" из группы ""%3""?'")
```

В то же время допустимым является:

1. Составление текста из нескольких предложений (каждое из которых заключено в **[[НСтр() - новая строка|НСтр]]** и переводится отдельно).
2. Предложения, заканчивающиеся двоеточием. Например, `НСтр("ru = 'Создание каталога не выполнено по причине:'")`.

В связи со сложившейся практикой, допускается использовать именованные параметры подстановки (параметры, включающие имя аналогично переменной, а не номер) только в двух вариантах: `[Параметр]`, `%Параметр%`. Здесь Параметр должен удовлетворять требованиям стандарта [Правила образования имен переменных](https://its.1c.ru/db/v8std/content/454/hdoc).

Правильно:

```js
СообщениеОНехватке = НСтр("ru='Не хватает товара %Товар% на складе %Склад%.'")
СообщениеОНехватке = СтрЗаменить(СообщениеОНехватке, "%Товар%", НаименованиеТовара);
СообщениеОНехватке = СтрЗаменить(СообщениеОНехватке, "%Склад%", НаименованиеСклада);
```

4. В функции **[[НСтр() - новая строка|НСтр]]** строка ограничивается символами одинарных кавычек. Такое требование обусловлено частым использованием двойных кавычек в строковых литералах, а также встроенным в платформу механизмом редактирования строк на разных языках.

Неправильно:

```js
ПоказатьПредупреждение(, НСтр("ru=Переменная типа ""Строка"""));
ПоказатьПредупреждение(, НСтр("ru=""Переменная типа ""Строка"""""));
```

Правильно:

```js
ПоказатьПредупреждение(, НСтр("ru='Переменная типа ""Строка""'"));
```
