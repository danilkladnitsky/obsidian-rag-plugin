---
tags:
  - 1С
---
## Задача

Формы списка объектов конфигурации построены с помощью динамического списка. Динамический список формируется путем запроса к основной таблице, указанной в соответствующем свойстве реквизита типа `ДинамическийСписок`, или путем произвольного запроса к базе данных.

Обычно далеко не все поля динамического списка используются для отображения в форме. Например, в форме списка справочника это поля `Код` и `Наименование` плюс реквизиты, созданные разработчиком. В запросе на сервере из таблицы справочника могут быть выбраны и переданы на клиент и другие его стандартные реквизиты. Например, для иерархического справочника это поля `Ссылка`, `ЭтоГруппа`, `Родитель`, `Предопределенный` и т.п.

Особенность в том, что запросом выбираются и передаются на клиент значения только тех реквизитов, в свойствах которых стоит отметка Использовать всегда или они отображаются в форме (рис. 4.46)

![[Pasted image 20240802160448.png]]

Таким образом, при помощи свойства полей динамического списка `Использовать всегда` можно получить значения реквизитов объекта на клиенте, не обращаясь лишний раз к серверу.

А можно «пойти» на сервер и получить эти реквизиты по ссылке на объект, используя свойство таблицы динамического списка `ТекущаяСтрока`.

Какой же способ наиболее эффективен? Рассмотрим этот вопрос на примере открытия текущего элемента из формы списка иерархического справочника.

Предположим, в конфигурации существует иерархический справочник `Товары`. При открытии формы элемента справочника из формы списка нужно проанализировать, является ли выбранный элемент группой, и в зависимости от этого открывать форму группы или форму элемента справочника.

Рассмотрим возможные варианты решения этой задачи.
## Первый вариант решения

В данном решении мы будем получать ссылку на текущий элемент, отображаемый в списке, затем «пойдем» с ней на сервер, чтобы из информационной базы узнать, является элемент группой или нет. В зависимости от этого, вернувшись на клиент, будем открывать либо форму элемента, либо форму группы.

Чтобы программно открывать форму элемента, создадим форму списка справочника Товары и ее команду `ОткрытьТовар`. Перетащим команду в командную панель формы. Обработчик команды `ОткрытьТовар` заполним следующим образом:

```js
&НаКлиенте
Процедура ОткрытьТовар(Команда)

	СсылкаНаЭлементСправочника = Элементы.Список.ТекущаяСтрока;
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаЭлементСправочника);

	Если ЕслиЭтоГруппа(СсылкаНаЭлементСправочника) Тогда
		ОткрытьФорму("Справочник.Товары.ФормаГруппы", ПараметрыФормы);
	Иначе
		ОткрытьФорму("Справочник.Товары.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры
```

В этом обработчике, используя свойство `ТекущаяСтрока` таблицы формы `Список`, отражающей данные динамического списка, мы получаем ссылку на текущий элемент справочника. Значение ссылки мы присваиваем параметру формы `Ключ`. И в зависимости от того, является ли данный элемент группой, открываем форму группы или форму элемента справочника с этим параметром.

Для определения того, является ли элемент справочника группой по переданной ссылке, поместим в модуле формы функцию, исполняющуюся на сервере без контекста формы:

```js
&НаСервереБезКонтекста
Функция ЕслиЭтоГруппа(Ссылка)

	Возврат Ссылка.ЭтоГруппа;

КонецФункции
```

Запустим «1С:Предприятие», выделим группу в форме списка товаров и нажмем кнопку `Открыть товар`. В результате будет открыта форма группы справочника товаров. Затем выделим элемент справочника и нажмем кнопку `Открыть товар`. В результате будет открыта форма элемента справочника товаров.

Однако в обоих случаях при открытии формы будут сделаны два обращения на сервер
(рис. 4.47).

![[Pasted image 20240802161350.png|500]]

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера
(рис. 4.48).

![[Pasted image 20240802161922.png]]

Второй вызов происходит при открытии формы элемента или группы справочника, и его делает сама платформа.

А вот первого вызова, который происходит при определении, является ли текущий элемент группой, могло бы и не быть. Ниже мы покажем, как этого избежать.
## Второй вариант решения

В данном решении мы не будем специально «ходить» на сервер и определять, является текущий элемент группой или нет, потому что эти данные уже есть в динамическом списке. Просто сделаем так, чтобы они присутствовали в нем, даже несмотря на то что они не отображаются в форме. Тогда все, что нам нужно знать об этом элементе, мы сможем выяснить прямо на клиенте, без вызова сервера.

Установим флажок `Использовать всегда` для поля `ЭтоГруппа` в свойствах основного реквизита формы `Список` (рис. 4.46). Это значит, что данные этого поля будут передаваться на клиент, даже несмотря на то что в форме они нигде не отображаются. Такая возможность сделана как раз на случай только программного использования этих данных.

Теперь изменим обработчик команды `ОткрытьТовар` следующим образом:

```js
&НаКлиенте
Процедура ОткрытьТовар(Команда)

	СсылкаНаЭлементСправочника = Элементы.Список.ТекущаяСтрока;
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаЭлементСправочника);

	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ОткрытьФорму("Справочник.Товары.ФормаГруппы", ПараметрыФормы);
	Иначе
		ОткрытьФорму("Справочник.Товары.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры
```

В этом обработчике для определения того, является ли элемент справочника группой, мы используем свойство `ТекущиеДанные` таблицы формы `Список`, отражающей данные динамического списка. И затем через точку от него обращаемся к полю `ЭтоГруппа` текущей строки этой таблицы.

Поскольку значение этого поля уже доступно на клиенте, нам не понадобится лишний раз обращаться на сервер. В результате та же функциональность будет реализована за один серверный вызов, и производительность прикладного решения будет выше (рис. 4.49)

![[Pasted image 20240802162411.png|500]]
## Резюме

В формах списка объектов конфигурации, построенных с помощью динамического списка, часто бывает нужно использовать значения стандартных реквизитов текущего объекта списка.

Для этого _нужно получать значения стандартных реквизитов объекта в динамических списках на клиенте, устанавливая флажок `Использовать всегда`, вместо того чтобы обращаться на сервер за этими значениями._

Таким образом, из показанных вариантов решения задачи более эффективным будет
второй – за счет получения реквизитов динамического списка _на клиенте без обращения к серверу_.

Тут есть один интересный момент. Заметьте, что в этой ситуации можно было бы использовать метод `ПоказатьЗначение(, Элементы. Список.ТекущаяСтрока)` – он один заменяет собой весь код выше (обработчик команды "Открыть товар"). Но в этом случае для иерархического справочника будет каждый раз происходить лишнее обращение к серверу для определения того, является открываемый элемент группой или нет. Но если это будет не иерархический справочник, а обычный, или другой объект конфигурации, документ например, этого обращения не будет.
