---
tags:
  - 1С
---
Для реализации функциональности форм разработчик может использовать клиентские и серверные обработчики событий формы. Но делать это нужно правильно и по назначению.

Например, при интерактивном или программном создании формы с существующими в информационной базе данными вызывается серверное событие `ПриЧтенииНаСервере`. Затем для всех форм вызывается серверное событие `ПриСозданииНаСервере`. В обработчиках этих двух событий можно и нужно подготавливать форму к работе.

Затем вызывается клиентское событие формы `ПриОткрытии`, в обработчике которого можно выполнять действия, доступные только на клиенте, например общение с пользователем.

Часто бывает нужно задать функциональность формы сразу при ее открытии – например, заполнить какие-то реквизиты формы или установить какие-то свойства ее элементов и т.п. В обработчике какого же события: `ПриСозданииНаСервере`, `ПриЧтенииНаСервере` или `ПриОткрытии` – это нужно делать? Рассмотрим пример.
## Задача

Предположим, в периодическом регистре сведений `Цены` содержатся розничные цены товаров из справочника `Товары`. При открытии формы уже существующего товара в поле Розничная цена должна подставляться цена из регистра сведений, и это поле должно быть недоступно для редактирования (рис. 4.42).

![[Pasted image 20240802004730.png|500]]
## Первый вариант решения

Чтобы обеспечить нужную функциональность формы при ее открытии, все действия будем выполнять в обработчике события `ПриОткрытии`. Подходящее название, зачем задумываться о назначении других обработчиков? «При открытии» – название события говорит само за себя, значит, в нем все и сделаем.

Для этого создадим форму элемента справочника `Товары`. Создадим реквизит формы `РозничнаяЦена` типа `Число` и перетащим его в дерево элементов формы. Затем создадим обработчик события формы `ПриОткрытии` и заполним его следующим образом:

```js
&НаКлиенте
Процедура ПриОткрытии(Отказ

	Если НЕ Параметры.Ключ.Пустая() Тогда
		РозничнаяЦена = РозничнаяЦена(Объект.Ссылка);
		Элементы.РозничнаяЦена.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры
```

В этом обработчике мы анализируем значение параметра формы `Ключ`. Если ссылка, содержащаяся в нем, непустая, то, значит, товар уже существует. Мы узнаем его цену на текущую дату из регистра сведений и присваиваем ее реквизиту `РозничнаяЦена`.

Также мы устанавливаем для поля формы `РозничнаяЦена` свойство `Доступность` в значение `Ложь`. Теперь поместим в модуль формы функцию для получения актуальной цены товара из регистра сведений, выполняющуюся на сервере без контекста формы:

```js
&НаСервереБезКонтекста
Функция РозничнаяЦена(ТоварСсылка)

	Отбор = Новый Структура;
	Отбор.Вставить("Товар", ТоварСсылка);
	ЗначенияРесурсов = РегистрыСведений.Цены.ПолучитьПоследнее( , Отбор);

	Возврат ЗначенияРесурсов.Цена;

КонецФункции
```

В этой функции мы создаем структуру `Отбор`, содержащую отбор по измерению регистра `Товар`, и устанавливаем его равным ссылке на товар, переданной в функцию. Затем при помощи метода `ПолучитьПоследнее()` мы возвращаем последнюю на текущую дату цену товара.

Запустим «1С:Предприятие» и откроем форму редактирования одного из товаров. В открывшейся форме поле Розничная цена автоматически заполнится последней ценой этого товара из регистра сведений, при этом поле Розничная цена будет недоступно для редактирования (рис. 4.42).

Однако при открытии формы редактирования существующего товара будут сделаны два обращения на сервер (рис. 4.43).

![[Pasted image 20240802005238.png|400]]

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера
(рис. 4.44).

![[Pasted image 20240802005354.png]]

Один вызов происходит при открытии формы элемента справочника, и его делает сама платформа.

А вот второго вызова, который происходит при подстановке цены из регистра сведений для существующего товара, могло бы и не быть. Этот лишний серверный вызов произошел с клиента, из обработчика события `ПриОткрытии`.

Ниже мы покажем, как этого избежать.
## Второй вариант решения

На самом деле, когда открывается форма с существующими в информационной базе данными, вызывается серверное событие `ПриЧтенииНаСервере`. В обработчике этого события, в параметре `ТекущийОбъект`, нам доступен прикладной объект, содержащийся в форме, со всей его функциональностью. Таким образом, здесь мы можем подготовить данные формы, зависящие от данных объекта, к открытию.

Затем вызывается серверное событие формы `ПриСозданииНаСервере`. Это событие не зависит от того, какие данные форма отображает, и вызывается при открытии у всех форм. Именно в обработчике этого события нужно полностью подготавливать саму форму, ее внешнее представление, к открытию. Для подготовки данных формы лучше использовать событие `ПриЧтенииНаСервере`.

Таким образом, к моменту передачи формы на клиент и возникновению события `ПриОткрытии` вся подготовительная работа по открытию формы будет уже сделана. Останется только выполнить какое-то общение с пользователем (показать предупреждение, задать вопрос и т.п.), если это нужно.

В данном решении мы используем обработчик события `ПриЧтенииНаСервере`. Это событие вызывается только для существующих объектов, при создании новых оно не вызывается.

Поэтому для реализации нужной функциональности формы мы можем стандартно сконфигурировать поле `РозничнаяЦена` недоступным.

Итак, создадим обработчик события формы элемента справочника Товары `ПриЧтенииНаСервере` и переместим в него код из функции `РозничнаяЦена()`:

```js
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	Отбор = Новый Структура;
	Отбор.Вставить("Товар", ТекущийОбъект.Ссылка);
	ЗначенияРесурсов = РегистрыСведений.Цены.ПолучитьПоследнее( , Отбор);
	РозничнаяЦена = ЗначенияРесурсов.Цена;

КонецПроцедуры
```

Отключим свойство `Доступность` для поля `РозничнаяЦена`. Поскольку обработчик `ПриЧтенииНаСервере` будет отрабатывать только для существующих объектов, то для существующих товаров мы заполняем поле `РозничнаяЦена` последней ценой текущего товара из регистра сведений.

Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет выше, так как не будет лишних вызовов сервера с клиента. В результате та же функциональность формы будет реализована за один серверный вызов (рис. 4.45)

![[Pasted image 20240802010151.png|400]]
## Резюме

Для реализации функциональности форм разработчик должен правильно и по назначению использовать клиентские и серверные обработчики событий формы.

_Большая часть кода должна быть реализована в серверных обработчиках событий формы. Например, форма должна быть максимально подготовлена для открытия в обработчиках событий `ПриЧтенииНаСервере` и `ПриСозданииНаСервере`, а в обработчике `ПриОткрытии` нужно выполнять только действия, недоступные на сервере: показать предупреждение, задать вопрос и т.п. _

_При открытии формы настоятельно не рекомендуется выполнять обращения к серверу из кода модуля формы в обработчиках клиентских событий формы, таких как `ПриОткрытии` и `ПриПовторномОткрытии`. При необходимости обращения из них к серверным данным следует размещать эти данные в реквизитах формы, в обработчике события `ПриСозданииНаСервере`._

Таким образом, из показанных вариантов решения задачи более эффективным будет второй – за счет реализации функциональности формы в обработчике события `ПриЧтенииНаСервере`.
