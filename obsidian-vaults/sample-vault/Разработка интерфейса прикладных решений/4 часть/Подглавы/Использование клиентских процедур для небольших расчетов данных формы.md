---
tags:
  - 1С
---
## Задача

В процессе разработки конфигурации часто возникает вопрос: «Где выполнять пересчет данных формы: на клиенте или на сервере?» Однозначного ответа тут нет.

С одной стороны, _на клиенте должно быть минимум кода_, потому что такова идеология клиентского приложения. Клиентского кода по определению не должно быть много. Основной подход заключается в том, что на клиенте должен исполняться только тот код, который управляет формой и отображением данных в ней. Все расчеты должны выполняться на сервере.

С другой стороны, _нужно избегать лишних обращений на сервер_. И хотя процесс вычислений произойдет на сервере значительно быстрее, но сам вызов сервера, да еще с контекстом формы, снизит производительность прикладного решения.

Решение зависит от конкретной задачи. Рассмотрим пример.

Предположим, в форме документа об оказании услуг содержатся цена услуги, процент вознаграждения и сумма услуги. При изменении цены услуги нужно рассчитать сумму услуги, равную цене услуги плюс процент вознаграждения. Процент вознаграждения устанавливается в зависимости от цены услуги (рис. 4.27).

![[Pasted image 20240731173245.png|600]]

Таким образом, в тот момент, когда пользователь изменит поле `ЦенаУслуги` документа `ОказаниеУслуги`, поля `Вознаграждение` и `Сумма` нужно рассчитать по описанному выше алгоритму.

Рассмотрим возможные варианты решения этой задачи.
## Первый вариант решения

Чтобы обеспечить перерасчет суммы услуги при изменении цены, мы сразу пойдем на сервер (ведь расчеты нужно делать на сервере), там рассчитаем вознаграждение и сумму услуги и поместим их в форму. Затем вернемся на клиент с готовой формой.

Для этого создадим форму документа `ОказаниеУслуги` и обработчик события `ПриИзменении()` для поля формы `ЦенаУслуги`:

```js
&НаКлиенте 
Процедура ЦенаУслугиПриИзменении(Элемент) 
	РасчетСуммыУслуги(); 
КонецПроцедуры
```

В этом обработчике мы вызываем процедуру `РасчетСуммыУслуги()`, исполняющуюся на сервере:

```js
&НаСервере 
Процедура РасчетСуммыУслуги() 
	
	Если Объект.ЦенаУслуги > 1000 Тогда 
		Объект.Вознаграждение = 10; 
	Иначе 
		Объект.Вознаграждение = 5; 
	КонецЕсли; 
	
	Объект.Сумма = Объект.ЦенаУслуги + Объект.ЦенаУслуги 
		* Объект.Вознаграждение / 100; 

КонецПроцедуры
```

В процедуру расчета передается контекст формы, так как в ней изменяются реквизиты объекта `Вознаграждение` и `Сумма`. Отключим для соответствующих полей формы свойство `Доступность`, так как они рассчитываются при изменении поля `ЦенаУслуги`.

Запустим «1С:Предприятие», откроем документ об оказании услуг, выберем услугу и внесем цену услуги. После этого поля `Вознаграждение` и `Сумма` автоматически пересчитаются по заданному нами алгоритму (рис. 4.28).

![[Pasted image 20240731173741.png]]

В результате будет сделано одно обращение на сервер, кроме того, этот вызов будет контекстным (рис. 4.29).

![[Pasted image 20240731192804.png|400]]
## Второй вариант решения

Чтобы оценить правильность своих действий, можно проанализировать, что же было сделано. Ход рассуждений может быть примерно таким:
- «Контекстный вызов. А он нужен? Можно было обойтись внеконтекстным вызовом?
- «Внеконтекстный вызов. А он нужен? Вообще тут нужен вызов сервера? Есть что-то, что нельзя сделать на клиенте?»
- «Если это делать на клиенте, это сложные вычисления? Сложнее, чем если бы их делали на сервере?».

На самом деле обращения на сервер для пересчета данных формы могло бы и не быть. Как мы увидим ниже, более эффективно этот расчет будет работать на клиенте.

Поскольку все данные для пересчета доступны на клиенте (это поля основного реквизита формы `Объект` – `ЦенаУслуги`, `Вознаграждение` и `Сумма`), то нет смысла лишний раз обращаться на сервер. Код пересчета – небольшой, и он быстро выполнится из клиентской процедуры при изменении цены услуги.

Для этого изменим обработчик события `ПриИзменении()` поля `ЦенаУслуги` документа `ОказаниеУслуги` следующим образом:

```js
&НаКлиенте 
Процедура ЦенаУслугиПриИзменении(Элемент) 

	Если Объект.ЦенаУслуги > 1000 Тогда 
		Объект.Вознаграждение = 10; 
	Иначе 
		Объект.Вознаграждение = 5; 
	КонецЕсли; 
	
	Объект.Сумма = Объект.ЦенаУслуги + Объект.ЦенаУслуги 
		* Объект.Вознаграждение / 100; 

КонецПроцедуры
```

Запустим «1С:Предприятие», откроем документ об оказании услуг, выберем услугу и внесем цену услуги. После этого поля `Вознаграждение` и `Сумма` автоматически пересчитаются по заданному нами алгоритму.

Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет выше, так как мы выполнили перерасчет данных документа _на клиенте, без обращения к серверу_.
## Резюме

Таким образом, если в процедуре пересчета требуется массированная обработка данных формы или если в ней используются свойства и методы объектов, недоступных на клиенте, то выполнять пересчет данных формы нужно на сервере. Для этого необходимо поместить код в контекстную серверную процедуру модуля формы. Или поместить код в процедуру модуля объекта и вызывать этот метод объекта, предварительно конвертировав данные формы в объект.

_Если в расчете участвует немного данных формы и все эти данные уже есть на клиенте, то лучше это делать в клиентской процедуре модуля формы._

_Не следует выполнять на клиенте сложные алгоритмы, требующие значительных ресурсов компьютера._ В таких случаях выполнение алгоритма на клиенте может занимать гораздо больше времени, чем передача управления с клиента на сервер, выполнение алгоритма на сервере и возврат результата обратно на клиент.

Клиентский код должен максимально быстро сделать что-то на клиенте, без обращения к серверу. Он не должен содержать большой объем бизнес-логики. Например, быстро пересчитать сумму по количеству и цене или рассчитать значение какого-то реквизита на основе другого и т.п.

Поэтому _не нужно без необходимости использовать контекстные серверные процедуры для небольших расчетов данных формы_.

Таким образом, из показанных вариантов решения задачи более эффективным будет второй вариант за счет пересчета данных формы _на клиенте, без обращения к серверу_.