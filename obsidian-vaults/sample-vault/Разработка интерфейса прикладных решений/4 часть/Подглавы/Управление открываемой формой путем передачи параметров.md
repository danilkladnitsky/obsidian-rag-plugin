---
tags:
  - 1С
---
## Задача

Часто бывает нужно открыть форму не интерактивно, а из встроенного языка. Как правило, при этом в зависимости от различных условий форма должна иметь тот или иной вид.

Чтобы открыть форму, используется метод глобального контекста `ОткрытьФорму()`. При этом в форму передаются параметры – стандартные или созданные разработчиком. Они используются для того, чтобы открыть форму в некотором нужном состоянии.

Параметры формы представляют собой структуру, каждый элемент которой описывает один параметр формы. Эта структура передается в метод `ОткрытьФорму()` вторым параметром, и в результате форма открывается за один серверный вызов в нужном состоянии.

Другим способом является получение формы методом `ПолучитьФорму()`. Используя возвращенное этим методом значение объекта `УправляемаяФорма`, можно обращаться к ее свойствам и методам, а также к свойствам и методам ее элементов, чтобы подготовить форму к открытию в нужном состоянии. И затем открыть форму методом `Открыть()`.

Какой же способ наиболее эффективен? Рассмотрим пример.

Предположим, в конфигурации существуют иерархический справочник `Товары` и документ `ПриходнаяНакладная` с табличной частью `Товары`, содержащей перечень приходуемых товаров. При нажатии кнопки `Подбор`, расположенной в форме документа, должен производиться подбор товаров в табличную часть приходной накладной. При этом форма выбора из справочника `Товары` должна открываться в режиме множественного выбора, и иерархический список товаров должен быть представлен в виде дерева (рис. 4.38).


![[Pasted image 20240731203836.png]]

Заметим, что если форма выбора всегда должна открываться в заданном виде, то можно задать свойства формы в конфигураторе и ничего не программировать. Однако форма выбора может открываться из разных мест конфигурации, и ее внешний вид может зависеть от различных условий. В таком случае нужно программно формировать внешний вид формы при ее открытии.

Последний случай мы сейчас рассмотрим и изучим, как всегда, возможные варианты решения этой задачи.
## Первый вариант решения

В данном решении мы получим форму на клиенте, установим нужные свойства этой формы и затем откроем ее.

Чтобы обеспечить подбор товаров в табличную часть приходной накладной, создадим форму документа и ее команду `Подбор`. Перетащим команду в командную панель таблицы формы `Товары`. Обработчик команды `Подбор` заполним следующим образом.

```js
&НаКлиенте Процедура Подбор(Команда)

	ФормаВыбора = ПолучитьФорму("Справочник.Товары.ФормаВыбора", ,
		Элементы.Товары);
	ФормаВыбора.Элементы.Список.МножественныйВыбор = Истина;
	ФормаВыбора.Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
	ФормаВыбора.Открыть();

КонецПроцедуры
```

В этом обработчике мы получаем основную форму выбора справочника `Товары` как подчиненную таблице `Товары` формы документа `ПриходнаяНакладная`. Таким образом, переменная `ФормаВыбора` будет содержать объект `УправляемаяФорма`. Используя коллекцию элементов (`Элементы`) этой формы, мы устанавливаем свойство `МножественныйВыбор` таблицы формы `Список` в значение `Истина` и свойство `Отображение` в значение системного перечисления `ОтображениеТаблицы.Дерево`.

Тем самым мы добьемся того, что форма, отражающая иерархический список товаров, будет открываться в режиме множественного выбора и таблица списка будет представлена в виде дерева. И затем мы открываем форму выбора методом `Открыть()`.

При выборе из формы выбора справочника выбранное значение будет передано в обработчик события `ОбработкаВыбора` таблицы формы `Товары` приходной накладной, так как она является владельцем открытой формы выбора. Причем при множественном выборе форма возвращает не один элемент, а массив элементов.

Поэтому создадим обработчик события `ОбработкаВыбора` таблицы формы Товары и заполним его следующим образом:

```js
&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Для Каждого ВыбранныйЭлемент Из ВыбранноеЗначение Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.Товар = ВыбранныйЭлемент;
	КонецЦикла;

КонецПроцедуры
```

В этом обработчике мы организуем цикл обхода массива переданных элементов (`ВыбранноеЗначение`). И в этом цикле добавим новую строку в табличную часть документа (`Объект.Товары`) и присвоим ее полю `Товар` значение очередного элемента массива выбранных товаров.

Запустим «1С:Предприятие», откроем приходную накладную и нажмем кнопку `Подбор`. После этого форма выбора из справочника товаров будет открыта в режиме множественного выбора и иерархический список товаров будет представлен в виде дерева. При нажатии кнопки `Выбрать` выделенные записи из справочника товаров будут добавлены в табличную часть приходной накладной (рис. 4.38).

Если мы посмотрим на показатели производительности, то увидим, что при открытии формы выбора в процедуре `Подбор()` произойдут два вызова сервера (рис. 4.39).

![[Pasted image 20240731205807.png|400]]

> Заметим, что при первом открытии формы могут быть лишние серверные вызовы за счет кеширования платформой необходимой информации о форме. Но мы будем изучать стандартную ситуацию, когда при открытии формы происходит один вызов сервера.

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера (рис. 4.40).

![[Pasted image 20240731205944.png]]

Один вызов происходит при выполнении метода `ПолучитьФорму()`, и он оправдан, так как его делает сама платформа.

А вот второго вызова, который происходит при изменении свойства `Отображение` таблицы формы `Список`, могло бы и не быть.

Ниже мы покажем, как этого избежать.
## Второй вариант решения

На самом деле более эффективно открывать форму методом `ОткрытьФорму()`, при этом передавая в нее параметры. Параметры будут доступны в обработчике события формы выбора `ПриСозданииНаСервере`, где и нужно управлять состоянием формы при ее открытии.

Для этого изменим обработчик команды `Подбор` документа `ПриходнаяНакладная` следующим образом:

```js
&НаКлиенте
Процедура Подбор(Команда)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("РежимОтображения", "Дерево");

	ОткрытьФорму("Справочник.Товары.ФормаВыбора", ПараметрыФормы,
		Элементы.Товары);

КонецПроцедуры
```

В этом обработчике мы создаем структуру `ПараметрыФормы` и добавляем в нее элементы `МножественныйВыбор` со значением `Истина` и `РежимОтображения` со значением `Дерево`.

Причем `МножественныйВыбор` – это стандартный параметр, поставляемый расширением формы для динамического списка, который при открытии формы выбора устанавливает соответствующее свойство для таблицы динамического списка. Это платформа делает автоматически.

А параметр `РежимОтображения` мы создаем сами, чтобы передать в обработчик события `ПриСозданииНаСервере` формы выбора справочника режим отображения таблицы динамического списка в виде дерева.

Затем мы открываем основную форму выбора справочника `Товары` как подчиненную таблице `Товары` формы документа `ПриходнаяНакладная` методом `ОткрытьФорму()`, который производит одно обращение к серверу.

Для анализа параметра `РежимОтображения` создадим форму выбора справочника `Товары` и обработчик события формы `ПриСозданииНаСервере`. Заполним его следующим образом:

```js
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.РежимОтображения = "Дерево" Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;

КонецПроцедуры
```

В этом обработчике мы анализируем значение параметра `РежимОтображения` и в соответствии с ним устанавливаем свойство `Отображение` таблицы формы `Список` в значение системного перечисления `ОтображениеТаблицы.Дерево`.

Запустим «1С:Предприятие», откроем приходную накладную и нажмем кнопку `Подбор`. После этого форма выбора из справочника товаров будет открыта в режиме множественного выбора, и иерархический список товаров будет представлен в виде дерева. При нажатии кнопки `Выбрать` выделенные записи из справочника товаров будут добавлены в табличную часть приходной накладной.

Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет выше, так как для открытия формы выбора при помощи метода `ОткрытьФорму()` потребовался только один вызов сервера (рис. 4.41).

![[Pasted image 20240731210523.png|400]]
## Резюме

Чтобы открыть форму в некотором нужном состоянии, рекомендуется использовать метод `ОткрытьФорму()` и при этом передавать в него параметры. Форма будет открыта за один серверный вызов.

Не рекомендуется открывать форму с помощью метода `ПолучитьФорму()` и затем обращаться к ее свойствам и методам, так как изменение свойств и методов формы на клиенте может привести к лишним обращениям на сервер.

Кроме того, программный код будет выглядеть при этом менее стройным и читаемым. Он может вообще перестать работать, если, например, в форме переименовали какой-то реквизит, к которому обращались снаружи.

Таким образом, из показанных вариантов решения задачи более эффективным будет второй – за счет открытия формы с передачей в нее параметров за один серверный вызов.
