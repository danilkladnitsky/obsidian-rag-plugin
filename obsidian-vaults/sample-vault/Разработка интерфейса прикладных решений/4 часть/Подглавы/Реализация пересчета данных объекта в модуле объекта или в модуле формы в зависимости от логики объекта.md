---
tags:
  - 1С
---
## Задача

Для реализации той или иной функциональности объектов конфигурации часто бывает нужно выполнить пересчет данных объекта, содержащегося в форме. Сделать это можно как в контекстной серверной процедуре модуля формы, так и в экспортируемой процедуре модуля объекта. 

В первом случае производится пересчет непосредственно данных формы без получения объекта, а при записи объекта из формы эти данные конвертируются в объект и записываются.

Во втором случае предварительно нужно получить объект, преобразовав данные формы в объект, произвести пересчет и выполнить обратное преобразование объекта в данные формы. На это требуется дополнительное время, но зато процедура, инкапсулированная в объекте, может вызываться из других мест конфигурации как отдельный метод этого объекта. 

Когда же нужно помещать процедуру пересчета данных объекта в модуле формы, а когда в модуле объекта? Однозначного ответа тут нет. Решение зависит от конкретной задачи. Рассмотрим пример.

Предположим, в конфигурации существует справочник `Клиенты`. В нем для каждого клиента хранится процент скидки, предоставляемый ему при оказании услуг. В форме документа об оказании услуг содержатся услуга, цена услуги, клиент, которому она оказывается, сумма скидки и сумма услуги. При выборе клиента нужно выяснить, предоставляется ли ему скидка, и рассчитать сумму скидки как процент от цены услуги. При этом сумма услуги рассчитывается как цена услуги за вычетом суммы скидки (рис. 4.73). 

![[Pasted image 20240805000958.png]]

Таким образом, в тот момент, когда пользователь изменит поле `Клиент` или поле `ЦенаУслуги` документа `ОказаниеУслуги`, поля `СуммаСкидки` и `СуммаУслуги` нужно рассчитать по описанному выше алгоритму. 

Рассмотрим возможные варианты решения этой задачи.
## Первый вариант решения

В данном решении поместим процедуру для начисления скидки клиенту в модуле формы документа `ОказаниеУслуги`. 

Чтобы обеспечить перерасчет суммы скидки и суммы услуги при изменении ее цены и выборе клиента, создадим форму документа `ОказаниеУслуги` и обработчик события `ПриИзменении` для поля формы `Клиент`, ссылающегося на справочник `Клиенты`:

```js
&НаКлиенте 
Процедура КлиентПриИзменении(Элемент) 
	НачислитьСкидку(); 
КонецПроцедуры
```

В этом обработчике мы вызываем контекстную серверную процедуру для начисления скидки клиенту, расположенную в модуле формы:

```js
&НаСервере 
Процедура НачислитьСкидку() 

	Объект.СуммаСкидки = Объект.ЦенаУслуги * Объект.Клиент.Скидка / 100; 
	Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
	
КонецПроцедуры
```

В этой процедуре мы получаем значение реквизита `Скидка` по ссылке на текущий элемент справочника `Клиенты` (`Объект.Клиент`). Как показывалось во втором варианте первого примера, более эффективно получать значение реквизита запросом, а не через точку от ссылки, но мы для упрощения сейчас сделаем так. 

И затем пересчитываем поля `СуммаСкидки` и `СуммаУслуги` документа `ОказаниеУслуги` по заданному алгоритму. 

Теперь обеспечим пересчет суммы услуги при изменении цены услуги. Для этого создадим обработчик события `ПриИзменении` для поля формы `ЦенаУслуги`:

```js
&НаКлиенте 
Процедура ЦенаУслугиПриИзменении(Элемент) 
	Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
КонецПроцедуры
```

Включим для полей формы `СуммаСкидки` и `СуммаУслуги` свойство `ТолькоПросмотр`, так как они являются расчетными. 

Запустим «1С:Предприятие», создадим документ об оказании услуги, выберем услугу и внесем цену услуги. При этом сумма услуги становится равной цене услуги. Затем выберем клиента. После этого, если в списке клиентов указан процент скидки, предоставляемой клиенту, сумма скидки рассчитывается как этот процент от цены услуги. При этом сумма услуги рассчитывается как цена услуги за вычетом суммы скидки (рис. 4.73).

Посмотрим теперь на показатели производительности. При выборе клиента *произойдет один вызов сервера* (рис. 4.74).

![[Pasted image 20240805001604.png|500]]

Этот вызов происходит при выполнении контекстной серверной процедуры `НачислитьСкидку()`. 

Итак, мы поместили процедуру для начисления скидки клиенту в модуле формы документа `ОказаниеУслуги`. После записи или проведения документа из формы пересчитанные данные формы будут преобразованы в объект и записаны. Таким образом, в данном решении мы посчитали процедуру начисления скидки «частным» делом этой формы. Снаружи данная функциональность формы не видна.
## Второй вариант решения

На самом деле может быть более логично поместить процедуру для начисления скидки клиенту в модуле объекта. Потому что начисление скидки – это часть прикладной логики самого документа `ОказаниеУслуги`. 

Изменим процедуру `НачислитьСкидку()` в модуле формы документа следующим образом:

```js
&НаСервере 
Процедура НачислитьСкидку() 

	Документ = РеквизитФормыВЗначение("Объект"); 
	Документ.НачислитьСкидкуКлиенту(); 
	ЗначениеВРеквизитФормы(Документ, "Объект"); 
	
КонецПроцедуры
```

В этой процедуре мы сначала получаем объект, отображающийся в форме. Для этого преобразуем основной реквизит формы в объект – документ `ОказаниеУслуги`. Затем выполняем экспортируемый метод объекта `НачислитьСкидкуКлиенту()` и выполняем обратное преобразование объекта в реквизит формы `Объект`. 

Затем откроем модуль объекта и поместим в нем процедуру для начисления скидки клиенту:

```js
Процедура НачислитьСкидкуКлиенту() Экспорт 

	СуммаСкидки = ЦенаУслуги * Клиент.Скидка / 100; 
	СуммаУслуги = ЦенаУслуги - СуммаСкидки; 
	
КонецПроцедуры
```

В этой процедуре мы реализуем тот же алгоритм пересчета, что и в предыдущем варианте, но только пересчитываем не данные формы, а данные самого объекта. Эти изменения после преобразования объекта в основной реквизит формы отобразятся в форме документа. 

Запустим «1С:Предприятие», создадим документ об оказании услуги, выберем услугу и внесем цену услуги. При этом сумма услуги становится равной цене услуги. Затем выберем клиента. После этого, если в списке клиентов указан процент скидки, предоставляемой клиенту, сумма скидки рассчитывается как этот процент от цены услуги. При этом сумма услуги рассчитывается как цена услуги за вычетом суммы скидки (рис. 4.73).

Посмотрим теперь на показатели производительности. При выборе клиента произойдет один вызов сервера (рис. 4.75).

![[Pasted image 20240805002141.png|500]]

Этот вызов происходит при выполнении контекстной серверной процедуры модуля формы `НачислитьСкидку()`, а из нее вызывается экспортируемый метод документа `НачислитьСкидкуКлиенту()`. 

Итак, мы поместили процедуру для начисления скидки клиенту в модуле объекта. Тем самым мы предоставили доступ к данному методу документа `ОказаниеУслуги` из других мест конфигурации: форм, обработок и т.д. Таким образом, в данном решении мы посчитали процедуру начисления скидки частью прикладной логики объекта.

Данная функциональность документа доступна, например, из формы списка документов об оказании услуг. То есть мы можем изменить клиенту процент скидки, затем выделить документ об оказании услуги и пересчитать по нему скидку, не открывая документа. 

Для этого создадим форму списка документа `ОказаниеУслуги` и ее команду `НачислитьСкидку`. Перетащим команду в командную панель формы. Обработчик команды `НачислитьСкидку` заполним следующим образом:

```js
&НаКлиенте 
Процедура НачислитьСкидку(Команда) 
	НачислитьСкидкуНаСервере(Элементы.Список.ТекущаяСтрока); 
КонецПроцедуры
```

В этом обработчике мы вызываем внеконтекстную серверную процедуру модуля формы `НачислитьСкидкуНаСервере()` и передаем в нее ссылку на текущий документ:

```js
&НаСервереБезКонтекста 
Процедура НачислитьСкидкуНаСервере(СсылкаНаДокумент) 

	Документ = СсылкаНаДокумент.ПолучитьОбъект(); 
	Документ.НачислитьСкидкуКлиенту(); 
	Документ.Записать(); 
	
КонецПроцедуры
```

В этой процедуре по переданной ссылке на документ мы получаем объект, выполняем экспортируемый метод объекта `НачислитьСкидкуКлиенту()` и записываем документ. 

Запустим «1С:Предприятие», откроем список клиентов и изменим процент скидки, предоставляемой какому-либо клиенту. Затем откроем список документов об оказании услуг, выделим документ для данного клиента и нажмем кнопку Начислить скидку. Сумма скидки и сумма услуги в документе пересчитаются по заданному алгоритму (рис. 4.76).

![[Pasted image 20240805002612.png]]

Можно организовать начисление скидки для документов в цикле и т.д.
## Резюме

Пересчитывать данные объекта можно как в контекстной серверной процедуре модуля формы объекта, так и в экспортируемой процедуре модуля объекта. 

*Когда процедура пересчета является частью прикладной логики объекта, нужно реализовывать процедуру в модуле объекта как его экспортируемый метод.* 

*Когда процедура пересчета отражает функциональность конкретной формы, можно реализовывать ее в модуле этой формы.* 

Таким образом, из показанных вариантов решения задачи более эффективным будет 
второй – за счет реализации пересчета данных в экспортируемом методе объекта. Этот метод будет доступен из других мест конфигурации: форм, обработок и т.д.