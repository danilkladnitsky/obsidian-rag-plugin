---
tags:
  - 1С
---
## Задача

В процессе работы формы часто бывает нужно получить дополнительную информацию об объекте, на который ссылается поле реквизита, и отразить ее в форме. Как это сделать наиболее эффективно? Рассмотрим пример.

Предположим, в конфигурации существует справочник Товары. В нем для каждого товара хранятся его артикул, единица измерения и цена. Для приходования товаров существует документ `ПриходнаяНакладная`, в табличную часть которого подбираются товары (рис. 4.14).

![[Pasted image 20240731130313.png]]

Хочется, чтобы пользователь, выбрав товар, видел в документе не только ссылку, но сразу же и артикул, единицу измерения и цену выбранного товара (рис. 4.15).

![[Pasted image 20240731130411.png]]

Таким образом, в тот момент, когда пользователь выбирает товар в колонке Товар, ссылающейся на справочник Товары, в другие колонки табличной части документа Артикул, ЕдИзм и Цена нужно записать артикул, единицу измерения и цену этого товара, хранящиеся в справочнике.

Выбор товара происходит на клиенте, но на клиенте, имея ссылку на товар, нельзя получить значения реквизитов от этой ссылки, например Артикул.

Чтобы его получить, нужно «пойти» с этой ссылкой на сервер, там прочитать из базы данных значение артикула и передать его обратно на клиент.

Рассмотрим возможные варианты решения этой задачи.
## Первый вариант решения

Как уже говорилось выше, возможности ссылки на клиенте сильно ограничены. Чтобы воспользоваться полными возможностями ссылки, можно, например, создать универсальную функцию `ПолучитьРеквизитИзБазыДанных(Ссылка, ИмяРеквизита)`, поместить ее в общем модуле, исполняющемся на сервере, и использовать ее всякий раз, когда потребуется получить реквизит какого-нибудь объекта.

Поскольку функция универсальная, мы будем использовать ее в разных формах, поэтому поместим ее не в какой-то одной форме, а в общем модуле, чтобы все формы могли ею пользоваться.

Итак, создадим общий модуль конфигурации `РаботаСОбъектами`. По умолчанию у него установлен флажок Сервер, значит, экземпляры этого модуля будут скомпилированы на стороне сервера. Установим у него флажок Вызов сервера, чтобы экспортируемые процедуры и функции этого модуля можно было вызывать с клиента (рис. 4.16).

![[Pasted image 20240731130736.png|500]]

Поместим в этом модуле функцию для получения реквизита объекта по его ссылке:

```js
Функция ПолучитьРеквизитИзБазыДанных(Ссылка, ИмяРеквизита) Экспорт
	Возврат Ссылка[ИмяРеквизита]; 
КонецФункции
```

Чтобы обеспечить заполнение колонок табличной части приходной накладной при выборе товара, создадим форму документа и обработчик события `ПриИзменении()` для поля формы Товар, содержащегося в таблице формы `Товары`:

```js
&НаКлиенте 
Процедура ТоварыТоварПриИзменении(Элемент) 

	ДанныеТекущейСтроки = Элементы.Товары.ТекущиеДанные; 
	ВыбранныйТовар = ДанныеТекущейСтроки.Товар; 
	ДанныеТекущейСтроки.Артикул = 
		РаботаСОбъектами.ПолучитьРеквизитИзБазыДанных(ВыбранныйТовар, "Артикул"); 
	ДанныеТекущейСтроки.ЕдИзм = 
		РаботаСОбъектами.ПолучитьРеквизитИзБазыДанных(ВыбранныйТовар, 
		"ЕдиницаИзмерения"); 
	ДанныеТекущейСтроки.Цена = 
		РаботаСОбъектами.ПолучитьРеквизитИзБазыДанных(ВыбранныйТовар, "Цена");
	 
КонецПроцедуры
```

В этом обработчике мы получаем доступ к данным текущей строки таблицы формы в переменной `ДанныеТекущейСтроки`. Чтобы получить значение, содержащееся в конкретном поле текущей строки, нужно через точку указать имя этого поля (`.Товар`). Так мы получаем ссылку на выбранный товар и вместе с именем нужного реквизита передаем ее в функцию `ПолучитьРеквизитИзБазыДанных()`, которая возвращает нам значение этого реквизита.

Посмотрим теперь, какие серверные вызовы будут происходить в режиме 1С:Предприятие в результате.

Для этого запустим «1С:Предприятие», откроем приходную накладную и сделаем выбор из справочника товаров в колонке Товар. После этого колонки табличной части Артикул, ЕдИзм и Цена будут автоматически заполнены соответствующими значениями из справочника Товары (рис. 4.17).

![[Pasted image 20240731131717.png]]

Сразу после выбора товара посмотрим на показатели производительности (рис. 4.18).

![[Pasted image 20240731131755.png|400]]

Там будет показано число вызовов сервера, в общем случае их будет три или больше. Больше трех вызовов может быть при первом выполнении этого действия, так как платформа выполняет запрос к информационной базе и кеширование необходимых данных. При втором и последующем выборе товара произойдет три вызова сервера.

Как мы уже говорили выше, когда платформа самостоятельно обращается к серверу, на это мы повлиять не можем. Поэтому подробно исследовать, почему серверных вызовов больше трех, мы не будем. Но три серверных вызова – это «наши вызовы», которые мы породили своим кодом. Рассмотрим подробнее, откуда они взялись.

Так происходит потому, что в результате использования универсальной функции `ПолучитьРеквизитИзБазыДанных()` будет происходить обращение на сервер столько раз, сколько понадобится получить реквизитов объекта. Таким образом, при выборе товара в строке табличной части документа произошло три вызова сервера, так как нам понадобилось узнать значение трех реквизитов выбранного товара.
## Второй вариант решения

На самом деле нет необходимости три раза «дергать» сервер. Ведь мы точно знаем, что каждый раз при добавлении нового товара нам понадобятся все три его реквизита. А значит, все их можно получить за один серверный вызов. Это будет более эффективным решением.

В данном случае универсальность выбранного нами решения нужно принести в жертву эффективности работы конкретной формы в конкретной ситуации. В данной ситуации прежде всего нас должна заботить не универсальность, а прикладная логика работы формы. Универсальность сама по себе хороша, но не в данном случае. То есть нет одного решения на все времена. В каждой форме нужно думать заново.

Для реализации этого решения в обработчике события `ПриИзменении()` поля Товар табличной части документа `ПриходнаяНакладная` нужно вызвать функцию, возвращающую в виде структуры сразу все нужные нам реквизиты выбранного элемента. Поэтому в модуле формы документа поместим функцию `ПолучитьРеквизитыТовара()`, выполняющуюся на сервере без контекста формы

```js
&НаСервереБезКонтекста 
Функция ПолучитьРеквизитыТовара(Ссылка) 

	РеквизитыТовара = Новый Структура; 
	РеквизитыТовара.Вставить("Артикул", Ссылка.Артикул); 
	РеквизитыТовара.Вставить("ЕдиницаИзмерения", Ссылка.ЕдиницаИзмерения); 
	РеквизитыТовара.Вставить("Цена", Ссылка.Цена); 
	Возврат РеквизитыТовара; 
	
КонецФункции
```

В этой функции мы создаем структуру и заполняем ее поля Артикул, `ЕдиницаИзмерения` и Цена значениями соответствующих реквизитов справочника Товары, полученных по ссылке на элемент справочника.

Единственное значение данных формы, которое нам понадобится в этой функции, – это ссылка на элемент справочника, его мы передаем в параметре Ссылка. Поэтому мы выполняем внеконтекстный серверный вызов (директива компиляции `&НаСервереБезКонтекста`), который будет работать значительно быстрее, чем вызов сервера с контекстом формы (директива компиляции `&НаСервере`). Подробнее этот вопрос будет рассмотрен в следующем примере.

Как видите, функция будет совершенно не универсальная, зато форма будет работать быстро за счет минимизации серверных вызовов. То есть вместо трех вызовов сервера, как раньше, будет происходить всего один серверный вызов.

Теперь изменим обработчик события `ПриИзменении()` поля Товар табличной части документа `ПриходнаяНакладная` следующим образом

```js
&НаКлиенте 
Процедура ТоварыТоварПриИзменении(Элемент) 

	ДанныеТекущейСтроки = Элементы.Товары.ТекущиеДанные; 
	ВыбранныйТовар = ДанныеТекущейСтроки.Товар; 
	РеквизитыТовара = ПолучитьРеквизитыТовара(ВыбранныйТовар); 
	ДанныеТекущейСтроки.Артикул = РеквизитыТовара.Артикул; 
	ДанныеТекущейСтроки.ЕдИзм = РеквизитыТовара.ЕдиницаИзмерения; 
	ДанныеТекущейСтроки.Цена = РеквизитыТовара.Цена; 
	
КонецПроцедуры
```

В этом обработчике мы передаем ссылку на выбранный товар в функцию `ПолучитьРеквизитыТовара()`, которая возвращает в виде структуры реквизиты товара, и присваиваем значение полей возвращенной структуры колонкам табличной части Артикул, ЕдИзм и Цена.

Запустим «1С:Предприятие», откроем приходную накладную и сделаем выбор из справочника товаров в колонке Товар табличной части документа. После этого колонки табличной части Артикул, ЕдИзм и Цена будут автоматически заполнены соответствующими значениями из справочника Товары (рис. 4.19).

![[Pasted image 20240731132532.png]]

Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет втрое выше, так как при выборе товара в одной строке табличной части документа будет произведено только одно обращение к серверу (рис. 4.20).

![[Pasted image 20240731132602.png|400]]

Таким образом, мы видим реальный выигрыш в производительности прикладного решения по сравнению с предыдущим вариантом.

> Заметим, что здесь, так же как и в предыдущем варианте, при первом выборе товара может быть лишний серверный вызов за счет кеширования формой данных в списке выбора.

Однако заметьте, что мы получали реквизиты товара по его ссылке. Эта возможность платформы очень удобная, но приводящая к загрузке всего объекта в память, что не всегда нужно. Допустим, нам нужно получить только три реквизита товара, а всего их – пятьдесят. Поэтому эффективнее использовать запрос для получения реквизитов товара.

В связи с этим изменим функцию для получения реквизитов товара в модуле формы следующим образом:

```js
&НаСервереБезКонтекста 
Функция ПолучитьРеквизитыТовара(ТоварСсылка) 
	
	РеквизитыТовара = Новый Структура; 
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ 
		| Товары.Артикул, 
		| Товары.ЕдиницаИзмерения, 
		| Товары.Цена 
		|ИЗ 
		| Справочник.Товары КАК Товары 
		|ГДЕ 
		| Товары.Ссылка = &Ссылка"; 
	Запрос.УстановитьПараметр("Ссылка", ТоварСсылка); 
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Если Выборка.Следующий() Тогда 
		Артикул = Выборка.Артикул;
		ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения; 
		Цена = Выборка.Цена; 
	Иначе 
		Артикул = 0; 
		ЕдиницаИзмерения = 0; 
		Цена = 0; 
	КонецЕсли; 
	
	РеквизитыТовара.Вставить("Артикул", Артикул); 
	РеквизитыТовара.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения); 
	РеквизитыТовара.Вставить("Цена", Цена); 
	
	Возврат РеквизитыТовара; 
	
КонецФункции
```

В этой функции мы заполняем поля структуры реквизитов товара значениями соответствующих реквизитов, полученных при помощи запроса из справочника Товары. В запросе мы устанавливаем параметр `Ссылка`, равный переданной в функцию ссылке на элемент справочника (`ТоварСсылка`).
## Резюме

В процессе работы прикладного решения любое обращение на сервер сказывается на его производительности.

Поэтому нужно стараться получить всю необходимую информацию за один вызов сервера.

Не нужно создавать универсальные процедуры для обхода клиент-серверной логики, например `ПолучитьРеквизитИзБазыДанных(Ссылка, ИмяРеквизита)`, и вызывать их всякий раз, когда понадобятся какие-либо реквизиты объекта.

Таким образом, из показанных вариантов решения задачи более эффективным будет второй вариант за счет меньшего количества серверных вызовов.

Заметим, что объединять в один серверный вызов нужно логически связанные действия, иначе может возникнуть желание вообще все сделать за один вызов сервера, даже то, что логически между собой не связано.