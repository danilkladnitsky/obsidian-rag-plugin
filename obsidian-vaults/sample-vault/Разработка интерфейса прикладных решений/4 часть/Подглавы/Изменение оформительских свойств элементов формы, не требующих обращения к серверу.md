---
tags:
  - 1С
---
## Задача

Для реализации той или иной функциональности формы часто бывает нужно изменять свойства ее элементов (например, менять их оформление) в зависимости от конкретных действий пользователя. 

Первое, что приходит в голову, – использовать для этого обработчик события элементов формы `ПриИзменении` и программно изменять требуемые свойства на клиенте. 

Но нужно учитывать, что если свойства, влияющие на размер элементов формы, меняются программно (например, свойство `Шрифт`), то форма должна полностью «пересчитать» расположение полей. Это выполняется на сервере и требует определенных затрат времени.

В платформе «1С:Предприятие» начиная с версии 8.3.7 реализован новый механизм расстановки элементов формы. В результате реализованы новые возможности размещения элементов в форме, изменение большинства оформительских свойств элементов формы на клиенте больше не требует обращения к серверу.

Поэтому при программном изменении на клиенте желательно использовать свойства элементов формы, изменение которых не приводит к обращениям на сервер. Например, вместо свойства `Шрифт` можно использовать свойства `ЦветФона` или `ЦветТекста`. 

Если же, в зависимости от действий пользователя, требуется изменять именно шрифт элемента, то для этого можно использовать условное оформление. При этом изменение шрифта элемента формы с помощью условного оформления не влияет на размер элемента и, соответственно, не приводит к обращению на сервер. 

Какой же вариант предпочтительнее? Рассмотрим пример.

Предположим, в форме документа об оказании услуг содержатся цена услуги, флажок скидки, сумма скидки и сумма услуги. В случае установки флажка `Скидка` поле `СуммаСкидки` должно быть выделено жирным шрифтом и зеленым цветом текста. При снятии флажка Скидка поле `СуммаСкидки` должно обнуляться и выводиться обычным шрифтом и красным цветом текста (рис. 4.77).

![[Pasted image 20240805003213.png]]

Кроме того, при изменении цены услуги и суммы скидки, в зависимости от состояния флажка `Скидка`, нужно рассчитать сумму услуги, равную цене услуги за вычетом суммы скидки. Сразу оговоримся, что прямого отношения к теме примера эта функциональность не имеет.
## Первый вариант решения

Чтобы реализовать поставленную задачу, создадим форму документа `ОказаниеУслуги` и обработчик события `ПриИзменении` для поля формы `Скидка`:

```js
&НаКлиенте 
Процедура СкидкаПриИзменении(Элемент) 

	Если Объект.Скидка Тогда
	 
		Элементы.СуммаСкидки.Шрифт = Новый Шрифт(,, Истина); 
		Элементы.СуммаСкидки.ЦветТекста = WebЦвета.ЗеленаяЛужайка; 
		Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
	
	Иначе 
		
		Элементы.СуммаСкидки.Шрифт = Новый Шрифт(); 
		Элементы.СуммаСкидки.ЦветТекста = WebЦвета.Коралловый; 
		Объект.СуммаСкидки = 0; 
		Объект.СуммаУслуги = Объект.ЦенаУслуги; 
	
	КонецЕсли; 
	
КонецПроцедуры
```

В этом обработчике мы анализируем значение реквизита документа `Скидка` типа `Булево` и в зависимости от него устанавливаем цвет и жирность шрифта для поля формы 
`СуммаСкидки`. Новый шрифт поля мы создаем конструктором – `Новый Шрифт()`, при создании которого в третьем параметре указывается жирность. 

Кроме того, при изменении флажка `Скидка` мы производим пересчет суммы услуги в соответствии с заданным алгоритмом. 

Теперь обеспечим пересчет суммы услуги при изменении цены услуги и суммы скидки, в зависимости от состояния флажка `Скидка`.

Создадим обработчик события `ПриИзменении` для поля формы `ЦенаУслуги` и заполним его следующим образом:

```js
&НаКлиенте 
Процедура ЦенаУслугиПриИзменении(Элемент) 

	Если Объект.Скидка Тогда 
		Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
	Иначе 
		Объект.СуммаУслуги = Объект.ЦенаУслуги; 
	КонецЕсли; 
	
КонецПроцедуры
```

Затем создадим обработчик события `ПриИзменении` для поля формы `СуммаСкидки` и заполним его следующим образом:

```js
&НаКлиенте 
Процедура СуммаСкидкиПриИзменении(Элемент) 

	Если Объект.Скидка Тогда 
		Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
	КонецЕсли; 
		
КонецПроцедуры
```

Алгоритм пересчета суммы услуги мы выполняем на клиенте, так как он небольшой и быстро выполняется из клиентской процедуры при изменении цены услуги и суммы скидки. 

Включим для поля формы `СуммаУслуги` свойство `ТолькоПросмотр`, так как оно является расчетным. 

И в заключение установим оформление поля `СуммаСкидки` в зависимости от отметки в поле `Скидка` в обработчике события формы `ПриСозданииНаСервере`:

```js
&НаСервере 
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 

	Если Объект.Скидка Тогда 
		
		Элементы.СуммаСкидки.Шрифт = Новый Шрифт(,, Истина); 
		Элементы.СуммаСкидки.ЦветТекста = WebЦвета.ЗеленаяЛужайка; 
		
	Иначе 
		
		Элементы.СуммаСкидки.Шрифт = Новый Шрифт(); 
		Элементы.СуммаСкидки.ЦветТекста = WebЦвета.Коралловый; 
		
	КонецЕсли; 
	
КонецПроцедуры
```

Запустим «1С:Предприятие», создадим документ об оказании услуги, выберем услугу, клиента и внесем цену услуги. Первоначально флажок `Скидка` не установлен, сумма услуги равна цене услуги, а сумма скидки отображается красным цветом текста обычной жирности. Внесем сумму скидки – сумма услуги не изменится. Установим флажок `Скидка`. После этого поле `СуммаСкидки` будет отображено жирным шрифтом и зеленым цветом текста, а поле `СуммаУслуги` автоматически пересчитается по заданному нами алгоритму (рис. 4.77).

При снятии флажка `Скидка` поле `СуммаСкидки` обнуляется и отображается красным, а поле `СуммаУслуги` снова пересчитывается. 

Посмотрим теперь на показатели производительности (рис. 4.78).

![[Pasted image 20240805010337.png|500]]

Пересчет поля `СуммаУслуги` происходит на клиенте и не требует обращения к серверу. Также «безболезненно» произойдет и изменение цвета текста поля `СуммаСкидки`. А вот при изменении свойства `Шрифт` на клиенте произойдет нежелательный вызов сервера, так как изменение этого свойства влияет на размер элемента формы. Это приведет к пересчету местоположения элементов формы, которое происходит на сервере. 

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера 
(рис. 4.79).

![[Pasted image 20240805010524.png]]
## Второй вариант решения

На самом деле можно обойтись без вызова сервера, если использовать для изменения шрифта поля `СуммаСкидки` условное оформление формы. 

Для этого зададим условное оформление поля `СуммаСкидки`: жирный шрифт и зеленый цвет текста, в случае если значение поля `Скидка` истинно (флажок установлен), и красный цвет текста, если значение поля `Скидка` ложно (рис. 4.80).

![[Pasted image 20240805011050.png]]

Теперь изменим обработчик события `ПриИзменении` поля формы `Скидка`:

```js
&НаКлиенте 
Процедура СкидкаПриИзменении(Элемент) 

	Если Объект.Скидка Тогда 
	
		Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
		
	Иначе 
		
		Объект.СуммаСкидки = 0; 
		Объект.СуммаУслуги = Объект.ЦенаУслуги; 
	
	КонецЕсли; 
	
КонецПроцедуры
```

В этом обработчике мы оставляем только пересчет суммы услуги в соответствии с заданным алгоритмом, а оформление поля мы задали с помощью условного оформления формы 
(рис. 4.80). 

Обработчик события формы `ПриСозданииНаСервере` можно теперь удалить, так как он больше не нужен. 

Запустим «1С:Предприятие», создадим документ об оказании услуги, выберем услугу, клиента и внесем цену услуги. Первоначально флажок `Скидка` не установлен, сумма услуги равна цене услуги, а сумма скидки отображается красным цветом текста обычной жирности. Внесем сумму скидки – сумма услуги не изменится. Установим флажок `Скидка`. После этого поле `СуммаСкидки` будет отображено жирным шрифтом и зеленым цветом текста, а поле `СуммаУслуги` автоматически пересчитается по заданному нами алгоритму (рис. 4.77).

При снятии флажка `Скидка` поле `СуммаСкидки` обнуляется и отображается красным, а поле `СуммаУслуги` снова пересчитывается. 

Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет выше, так как изменение шрифта поля формы с помощью условного оформления *не приведет к обращению на сервер*.
## Третий вариант решения

В качестве альтернативного варианта при изменении оформления поля `СуммаСкидки` можно использовать одно из свойств, изменение которых не требует обращения к серверу. Например, свойство `ЦветРамки` вместо свойства `Шрифт`. 

Попутно сделаем еще одно полезное усовершенствование. Дело в том, что программное изменение оформления поля `СуммаСкидки` выполняется два раза: при изменении поля `Скидка` (в обработчике события `ПриИзменении`) и при создании формы документа (в обработчике события `ПриСозданииНаСервере`). Поэтому в первом варианте решения один и тот же код дублировался в каждом из этих обработчиков.

Но лучше «навести порядок» в модуле формы и перенести код, изменяющий оформление поля, в отдельную процедуру. И затем вызывать ее из обработчиков событий `ПриИзменении` и `ПриСозданииНаСервере`. 

Какой же будет директива компиляции этой процедуры? Ведь она должна вызываться как с сервера, так и с клиента. Если мы предварим процедуру директивой `&НаКлиенте`, то мы не сможем вызывать ее с сервера, из обработчика события `ПриСозданииНаСервере`. Точнее, модуль формы не пройдет синтаксическую проверку в контексте сервера. 

Если мы напишем директиву `&НаСервере`, то получим лишний вызов сервера с клиента при возникновении события поля `ПриИзменении`. Хотя изменение оформительских свойств поля формы вполне может обойтись без вызова сервера.

Единственная подходящая директива – `&НаКлиентеНаСервереБезКонтекста`. Процедура, описанная такой директивой компиляции, будет выполняться то на сервере, 
то на клиенте – в зависимости от того, откуда она была вызвана. Но, поскольку процедура будет выполняться без контекста формы, мы должны самостоятельно передать этот контекст в параметре типа `УправляемаяФорма`. 

Итак, добавим в модуль формы процедуру `УстановитьОформление()` и перенесем в нее код, изменяющий оформление поля `СуммаСкидки` в зависимости от отметки в поле `Скидка`:

```js
&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьОформление(Форма) 

	Если Форма.Объект.Скидка Тогда 
		Форма.Элементы.СуммаСкидки.ЦветРамки = WebЦвета.ЗеленаяЛужайка; 
	Иначе 
		Форма.Элементы.СуммаСкидки.ЦветРамки = WebЦвета.Коралловый; 
	КонецЕсли; 
	
КонецПроцедуры
```

В этой процедуре в параметре `Форма` мы получаем контекст формы документа и, используя этот контекст, получаем доступ к коллекции элементов формы (`Форма.Элементы`) и к реквизиту документа `Скидка` (`Форма.Объект.Скидка`). 

Затем изменим обработчик события формы `ПриСозданииНаСервере`:

```js
&НаСервере 
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	УстановитьОформление(ЭтотОбъект); 
КонецПроцедуры
```

А в обработчике события `ПриИзменении` поля формы `Скидка` оставим только пересчет поля `СуммаУслуги`:

```js
&НаКлиенте 
Процедура СкидкаПриИзменении(Элемент) 
	
	Если Объект.Скидка Тогда 
		
		Объект.СуммаУслуги = Объект.ЦенаУслуги - Объект.СуммаСкидки; 
	
	Иначе 
		
		Объект.СуммаСкидки = 0; 
		Объект.СуммаУслуги = Объект.ЦенаУслуги; 
	
	КонецЕсли; 
	
	УстановитьОформление(ЭтотОбъект); 
	
КонецПроцедуры
```

Запустим «1С:Предприятие», откроем документ об оказании услуги, в котором установлена скидка. Флажок `Скидка` установлен, и поле `СуммаСкидки` обрамлено зеленой рамкой. При снятии отметки в поле Скидка рамка поля `СуммаСкидки` становится красной и значение поля обнуляется. После этого поле `СуммаУслуги` пересчитывается. Также оно пересчитывается при любом изменении цены услуги и суммы скидки в соответствии с заданным алгоритмом (рис. 4.81).

![[Pasted image 20240805013651.png]]

Как мы видим, функциональность прикладного решения будет практически такой же, как и в первом варианте, но производительность будет выше, так как изменение свойства `ЦветРамки` на клиенте *не приведет к обращению на сервер*.


> Заметим, что при первом изменении цвета рамки элементов формы могут быть лишние серверные вызовы за счет кеширования платформой необходимой информации.
## Резюме

Таким образом, если логика интерактивной работы формы требует изменения ее внешнего вида, *лучше использовать те свойства, изменение которых на клиенте не приводит к обращениям на сервер*. 

Размер шрифта учитывается при автоматическом определении размеров элементов формы, поэтому при программном изменении шрифта на клиенте выполняется обращение к серверу. 

Изменение размера шрифта, выполненное с помощью условного оформления формы и динамического списка, не влияет на размер элемента формы. Поэтому *рекомендуется заменить программное изменение шрифта на клиенте на использование условного оформления.*

*Рекомендуется не использовать формы, меняющие состав и положение элементов. И вообще, если есть такая возможность, лучше не использовать свойства и методы встроенного языка, приводящие к вызову сервера (это указано в синтакс-помощнике).* 

Таким образом, из показанных вариантов решения задачи более эффективным будут второй вариант – за счет изменения шрифта с помощью условного оформления, и третий – за счет изменения свойства поля формы `ЦветРамки` вместо свойства `Шрифт`. В результате в обоих случаях *оформление поля на клиенте не потребовало обращения к серверу*.