---
tags:
  - 1С
---
## Задача

Пользователю часто может понадобиться сформировать отчет сразу при его открытии. Эту очень удобную возможность предоставляет платформа «1С:Предприятие». 

Для этого можно использовать параметр `СформироватьПриОткрытии`, поставляемый расширением формы для отчета, и передавать его в форму при открытии. Или же можно установить этот параметр в значение `Истина` в обработчике события формы `ПриСозданииНаСервере`. 

Казалось бы, можно сформировать отчет и в обработчике события формы `ПриОткрытии` методом `СкомпоноватьРезультат()`, доступном в модуле формы на клиенте.

Но посмотрим, какое решение будет наиболее эффективным. 

Предположим, в конфигурации существует отчет `РеестрОказанныхУслуг`, выводящий список существующих в информационной базе документов `ОказаниеУслуги`. При открытии формы отчета список документов должен формироваться автоматически, без нажатия кнопки `Сформировать`. 

Для примера взят самый простой отчет, но он, тем не менее, позволит рассмотреть возможные варианты решения этой задачи.
## Первый вариант решения

Чтобы программно сформировать отчет `РеестрОказанныхУслуг` при открытии, в форме отчета, в обработчике события `ПриОткрытии`, вызовем автоматическое формирование отчета. 

Для этого создадим форму отчета и обработчик события формы `ПриОткрытии` и заполним его следующим образом:

```js
&НаКлиенте 
Процедура ПриОткрытии(Отказ) 
	СкомпоноватьРезультат(); 
КонецПроцедуры
```

В этом обработчике при открытии формы на клиенте отчет формируется методом `СкомпоноватьРезультат()`, поставляемым расширением формы для отчета. 

Запустим «1С:Предприятие» и откроем отчет `РеестрОказанныхУслуг` из группы команд Отчеты (рис. 4.50).

![[Pasted image 20240803152317.png]]

Отчет будет сформирован сразу при открытии формы. Вроде бы все хорошо, но в данном случае при открытии формы отчета будут сделаны два обращения на сервер (рис. 4.51).

![[Pasted image 20240803152347.png|500]]

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера 
(рис. 4.52). 

Один вызов происходит при открытии формы отчета, и его делает сама платформа. 

А вот второго вызова, который происходит при выполнении метода `СкомпоноватьРезультат()`, можно избежать.

![[Pasted image 20240803152511.png]]
## Второй вариант решения

На самом деле можно выполнить открытие формы отчета и его формирование за один серверный вызов. Для этого нужно установить параметр формы `СформироватьПриОткрытии` в значение `Истина` в обработчике события формы `ПриСозданииНаСервере`. 

Итак, создадим обработчик события формы отчета `РеестрОказанныхУслуг` – `ПриСозданииНаСервере` и заполним его следующим образом:

```js
&НаСервере 
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	Параметры.СформироватьПриОткрытии = Истина; 
КонецПроцедуры
```

А обработчик события `ПриОткрытии` удалим. 

Запустим «1С:Предприятие» и откроем отчет `РеестрОказанныхУслуг` из группы команд `Отчеты`. Как мы видим, функциональность прикладного решения будет такой же, как и в первом случае, но производительность будет выше, так как для открытия формы отчета и его формирования потребовался только один вызов сервера (рис. 4.53).

![[Pasted image 20240803153506.png|500]]
## Третий вариант решения

Для формирования отчета в первых двух случаях мы использовали стандартную команду открытия формы отчета, автоматически помещаемую платформой в глобальные команды приложения. При этом в обоих случаях отчет всегда будет формироваться при открытии с помощью этой стандартной команды. 

Теперь покажем вариант программного открытия формы отчета с параметром `СформироватьПриОткрытии`. 

Для этого создадим общую команду `СписокОказанныхУслуг` и поместим ее в группу 
`Панель навигации.Важное`.

В модуле команды напишем следующий код:

```js
&НаКлиенте 
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды) 
	
	ПараметрыФормы = Новый Структура("СформироватьПриОткрытии", Истина); 
	ОткрытьФорму("Отчет.РеестрОказанныхУслуг.Форма", ПараметрыФормы); 

КонецПроцедуры
```

В этом обработчике мы создаем структуру `ПараметрыФормы` и добавляем в нее элемент `СформироватьПриОткрытии` со значением `Истина`. Затем мы открываем форму отчета `РеестрОказанныхУслуг` с этим параметром. 

А форму отчета `РеестрОказанныхУслуг` можно теперь вообще удалить, поскольку она больше не нужна. А также уберем видимость у стандартной команды открытия формы отчета в командном интерфейсе конфигурации. 

Запустим «1С:Предприятие» и откроем отчет `РеестрОказанныхУслуг` из панели функций текущего раздела (рис. 4.54).

![[Pasted image 20240803153945.png]]

Отчет будет сформирован сразу при открытии формы _за один серверный вызов_.
## Резюме

Платформа «1С:Предприятие» предоставляет очень удобную возможность формировать отчет, построенный с помощью системы компоновки данных, сразу при его открытии. 

Для этого _нужно использовать параметр `СформироватьПриОткрытии`, поставляемый расширением формы для отчета, и передавать его в форму при открытии. Или же можно установить параметр формы `СформироватьПриОткрытии` в значение `Истина` в обработчике события формы `ПриСозданииНаСервере`. _

_При открытии формы отчета настоятельно не рекомендуется выполнять обращения к серверу, например формировать отчет, из кода модуля формы в обработчиках клиентских событий формы, таких как `ПриОткрытии` и `ПриПовторномОткрытии`. Это потребует лишнего обращения к серверу и будет неэффективно._

Таким образом, из показанных вариантов решения задачи более эффективными будут второй и третий – за счет использования параметра формы отчета `СформироватьПриОткрытии` и формирования отчета сразу при открытии _за один серверный вызов_.


