---
tags:
  - 1С
---
## Задача

В процессе работы прикладного решения значения реквизитов объекта могут меняться не только интерактивно, но и программно. При этом *запись данных объекта в информационную базу должна выполняться в единой транзакции за один серверный вызов*.

Рассмотрим этот вопрос на примере работы с файлами и картинками.

Предположим, в форме элемента справочника `Товары` содержится поле, отражающее картинку товара. При нажатии кнопки `Загрузить картинку`, расположенной в форме документа, должен открываться диалог выбора файла картинки. Выбранная картинка должна быть отображена в форме элемента и записана в соответствующий реквизит объекта
(рис. 4.60

![[Pasted image 20240804144641.png]]

Сначала пользователь должен выбрать картинку товара. Единственный способ работы с файлами пользователя – интерактивно выбрать картинку и поместить ее во временное хранилище на сервере.

Затем получить ее из временного хранилища и записать в базу данных, например в реквизит объекта.

В форме картинка отображается автоматически по навигационной ссылке на временное хранилище или на реквизит объекта, содержащий эту картинку.

Когда же нужно записывать картинку из временного хранилища в соответствующий реквизит объекта? Можно записать картинку сразу после выбора, но более эффективно записать ее тогда, когда пользователь захочет сохранить объект.

Рассмотрим возможные варианты решения этой задачи.
## Первый вариант решения

В данном решении запишем картинку в базу данных сразу же после ее выбора.

Итак, создадим форму элемента справочника Товары. Добавим в форму реквизит `АдресКартинки` (тип `Строка`), в котором будет храниться навигационная ссылка на реквизит справочника `ДанныеФайлаКартинки`. Перетащим реквизит `АдресКартинки` в дерево элементов формы. Установим свойства элемента: `Вид` – Поле картинки и `ПоложениеЗаголовка` – Нет.

Создадим команду формы `ЗагрузитьКартинку` и поместим ее в командную панель формы. При нажатии этой кнопки будут выполняться выбор файла картинки с диска, запись данных картинки в реквизит справочника `ДанныеФайлаКартинки` и отображение картинки в форме (рис. 4.61).

![[Pasted image 20240804145038.png]]

Обработчик команды `ЗагрузитьКартинку` заполним следующим образом

```js
&НаКлиенте
Процедура ЗагрузитьКартинку(Команда)

	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьКартинкуЗавершение",
		ЭтотОбъект), , , Истина , УникальныйИдентификатор);

КонецПроцедуры
```

Загрузка картинки выполняется с помощью немодального метода `НачатьПомещениеФайла()`.

В первом параметре этого метода описывается процедура обработки оповещения `ЗагрузитьКартинкуЗавершение()`, в которой в случае выбора файла картинки пользователем будут выполняться действия по загрузке картинки товара:

```js
&НаКлиенте
Процедура ЗагрузитьКартинкуЗавершение(Результат, АдресВХранилище,
									  ВыбранноеИмяФайла, ДополнительныеПараметры)
									  Экспорт

	Если Результат Тогда

		Файл = Новый Файл(ВыбранноеИмяФайла);
		Объект.ИмяФайла = Файл.Имя;
		АдресКартинки = АдресВХранилище;

		ПоместитьФайлКартинки();

	КонецЕсли;

	АдресКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка,
		"ДанныеФайлаКартинки");

КонецПроцедуры
```

В процедуру обработки оповещения в параметре `АдресВХранилище` передается адрес во временном хранилище, по которому был помещен файл картинки. В параметре `ВыбранноеИмяФайла` передается путь к выбранному файлу.

В случае успешного выбора картинки короткое имя файла сохраняется в реквизите справочника `ИмяФайла`, и данные файла картинки помещаются во временное хранилище. Адрес данных в этом хранилище сохраняется в реквизите формы `АдресКартинки`.

Затем в процедуре `ПоместитьФайлКартинки()` выполняется запись данных картинки в реквизит справочника `ДанныеФайлаКартинки`. И затем с помощью метода `ПолучитьНавигационнуюСсылку()` реквизиту `АдресКартинки` присваивается навигационная ссылка на реквизит `ДанныеФайлаКартинки`, и картинка отображается в форме. В первом параметре метода `ПолучитьНавигационнуюСсылку()` передается ссылка на объект `Объект.Ссылка`, а во втором – имя реквизита.

Контекстную серверную процедуру `ПоместитьФайлКартинки()` заполним следующим образом:

```js
&НаСервере
Процедура ПоместитьФайлКартинки()

	ЭлементСправочника = РеквизитФормыВЗначение("Объект");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
	ЭлементСправочника.ДанныеФайлаКартинки = Новый ХранилищеЗначения(
		ДвоичныеДанные, Новый СжатиеДанных());
	ЭлементСправочника.Записать();

	УдалитьИзВременногоХранилища(АдресКартинки);
	ЗначениеВРеквизитФормы(ЭлементСправочника, "Объект");

КонецПроцедуры
```

Чтобы мы могли записать объект, в этой процедуре значение основного реквизита формы `Объект` преобразуется в значение объекта – элемента справочника. Затем в переменной `ДвоичныеДанные` сохраняются данные, полученные из временного хранилища по адресу `АдресКартинки`. Затем создается объект `ХранилищеЗначения`, в него помещается значение переменной `ДвоичныеДанные`, и значение объекта `ХранилищеЗначения` сохраняется в реквизите справочника `ДанныеФайлаКартинки`. Затем данные объекта – элемента справочника записываются, временное хранилище очищается, и значение измененного объекта преобразовывается обратно в значение основного реквизита формы.

Теперь осталось обеспечить чтение картинки при чтении данных уже существующего объекта. Для этого создадим обработчик события формы `ПриЧтенииНаСервере` и заполним его следующим образом:

```js
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка,
		"ДанныеФайлаКартинки");

КонецПроцедуры
```

Запустим «1С:Предприятие» и откроем форму элемента справочника `Товары`. Если у товара уже была выбрана картинка, она отразится в форме. Нажмем кнопку `Загрузить картинку` и изменим картинку товара. Выбранная картинка отобразится в поле картинки товара и будет записана в реквизит объекта `ДанныеФайлаКартинки` (рис. 4.60).

Таким образом, мы добились нужной функциональности формы, но посмотрим теперь на показатели производительности. В результате из обработчика события нажатия кнопки `Загрузить картинку` будет сделано целых пять вызовов сервера! (рис. 4.62).

![[Pasted image 20240804150459.png|500]]

Для наглядности рассмотрим схему программного взаимодействия клиента и сервера
(рис. 4.63).

![[Pasted image 20240804150527.png]]

Два вызова сервера произойдут при выполнении метода `НачатьПомещениеФайла()`, и эти вызовы оправданны, так как их делает платформа. Один раз сервер вызывается при исполнении метода `НачатьПомещениеФайла()`, так как он передает картинку с клиентского компьютера во временное хранилище, а второй вызов происходит, потому что навигационная ссылка, возвращенная этим методом, сразу же помещается в реквизит формы `АдресКартинки`. Это приводит к тому, что форма обновляется, чтобы отобразить картинку по этой ссылке. Если в вызове метода `НачатьПомещениеФайла()` вместо `АдресКартинки` использовать другую произвольную переменную, а не реквизит формы, то произойдет только один серверный вызов.

Один вызов сервера будет сделан при записи картинки объекта при вызове серверной процедуры `ПоместитьФайлКартинки()`. Этого вызова можно избежать, если не записывать картинку сразу при выборе, а хранить данные картинки во временном хранилище до тех пор, пока объект не записан.

И еще два вызова произойдут при выполнении метода `ПолучитьНавигационнуюСсылку()`. Этот метод выполняет обращение к серверу при получении ссылки на реквизит. Здесь один серверный вызов происходит при получении навигационной ссылки, а второй – потому что она сразу же присваивается реквизиту формы `АдресКартинки`. Этих вызовов также можно избежать, если использовать навигационную ссылку на временное хранилище для отображения картинки в форме до тех пор, пока объект не записан.
## Второй вариант решения

Итак, устраним описанные выше ошибки. Для этого изменим обработчик команды `ЗагрузитьКартинку` следующим образом:

```js
&НаКлиенте
Процедура ЗагрузитьКартинку(Команда)

	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьКартинкуЗавершение",
		ЭтотОбъект), , , Истина , УникальныйИдентификатор);

КонецПроцедуры
```

В этом обработчике методом `НачатьПомещениеФайла()` открывается диалог выбора файла картинки с диска.

В процедуре обработки оповещения `ЗагрузитьКартинкуЗавершение()` в случае выбора файла картинки пользователем будут выполняться действия по загрузке картинки товара:

```js
&НаКлиенте
Процедура ЗагрузитьКартинкуЗавершение(Результат, АдресВХранилище,
									  ВыбранноеИмяФайла, ДополнительныеПараметры)
									  Экспорт

	Если Результат Тогда

		Файл = Новый Файл(ВыбранноеИмяФайла);
		Объект.ИмяФайла = Файл.Имя;
		АдресКартинки = АдресВХранилище;

		Модифицированность = Истина;

	КонецЕсли;

КонецПроцедуры
```

В случае успешного выбора картинки короткое имя выбранного файла (`ВыбранноеИмя`) сохраняется в реквизите справочника `ИмяФайла`, и данные файла картинки помещаются во временное хранилище (`АдресВХранилище`). Адрес данных в этом хранилище сохраняется в реквизите формы `АдресКартинки`.

Параметр `УникальныйИдентификатор` связывает эти данные хранилища с нашей формой. Когда форма будет закрыта, данные из хранилища будут автоматически удалены. Свойству формы `Модифицированность` присваивается значение `Истина`, чтобы указать, что форма уже модифицирована, так как мы выбрали картинку товара.

Как мы уже говорили, при выборе картинки данные еще не записаны в объект базы данных. Возможно, пользователь и не станет сохранять данные. В этом случае мы закроем форму, и данные временного хранилища, связанные с этой формой, будут автоматически уничтожены. Но если пользователь решит записать данные формы, то тогда нам нужно будет взять картинку из временного хранилища и сохранить ее в базе данных. Для этого создадим обработчик события формы `ПередЗаписьюНаСервере` и заполним его следующим образом:

```js
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда

		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
		ТекущийОбъект.ДанныеФайлаКартинки = Новый
			ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());

	КонецЕсли;

КонецПроцедуры
```

Это событие выполняется непосредственно перед записью объекта из формы, когда пользователь нажал `Записать` или `Записать и закрыть`.

В этой процедуре мы сохраняем значение картинки в том случае, если у нас есть ссылка на временное хранилище. В переменной `ДвоичныеДанные` сохраняются данные, полученные из временного хранилища по адресу `АдресКартинки`. Затем создается объект `ХранилищеЗначения`, в него помещается значение переменной `ДвоичныеДанные`, и значение объекта `ХранилищеЗначения` сохраняется в реквизите справочника `ДанныеФайлаКартинки`.

После записи картинки в объект нужно очистить временное хранилище и установить для реквизита формы `АдресКартинки` новую навигационную ссылку на реквизит объекта, хранящий данные картинки. Для этого создадим обработчик события формы `ПриЗаписиНаСервере` и заполним его следующим образом:

```js
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		УдалитьИзВременногоХранилища(АдресКартинки);
	КонецЕсли;

	АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка,
		"ДанныеФайлаКартинки");

КонецПроцедуры
```

Это событие выполняется после записи данных объекта, но еще до окончания транзакции записи. Так что если по каким-то причинам объект не был записан в базу, в форме останется все по-прежнему и реквизит `АдресКартинки` будет указывать на временное хранилище, в котором будет находиться картинка объекта.

Процедуру `ПоместитьФайлКартинки()` теперь можно удалить из модуля формы, так как она больше не нужна.

Запустим «1С:Предприятие» и откроем форму элемента справочника `Товары`. Если у товара уже была выбрана картинка, она отразится в форме. Нажмем кнопку `Загрузить картинку` и изменим картинку товара. Выбранная картинка отобразится в поле картинки товара и будет записана в реквизит объекта `ДанныеФайлаКартинки` в тот момент, когда пользователь решит записать товар.

В результате из обработчика события нажатия кнопки `Загрузить картинку` будут сделаны два вызова сервера (рис. 4.64).

![[Pasted image 20240804153426.png|500]]

Эти вызовы произойдут при выполнении метода `НачатьПомещениеФайла()`, и они оправданны, так как их делает платформа.

Посмотрим теперь, сколько вызовов сервера будет сделано при записи элемента справочника. Ведь именно в этот момент картинка окажется в базе данных. В форме товара нажмем кнопку `Записать` (рис. 4.65).

![[Pasted image 20240804153508.png|500]]

При первом нажатии произойдут три серверных вызова и затем – два вызова, если мы, не закрывая формы, снова и снова нажимаем кнопку Записать.

**Первый** вызов происходит, когда устанавливается объектная блокировка на записываемые данные. **Второй** контекстный вызов сервера выполняется платформой, чтобы записать данные, отображаемые в форме. **Третий** – при обновлении динамического списка, открытого в основном окне программы. При последующих нажатиях кнопки `Записать` выполняются последние два вызова. Блокировка устанавливается только один раз и будет снята при закрытии формы.

Таким образом, видно, что при записи выполняются «обычные» вызовы сервера, которые платформа сделала бы в любом случае. То есть на запись картинки товара мы не потратили дополнительных вызовов сервера.

В результате мы избавились от трех лишних вызовов сервера, которые были у нас в первом случае при вызове методов `ПоместитьФайлКартинки()` и `ПолучитьНавигационнуюСсылку()`.
## Резюме

Запись объектов конфигурации в информационную базу должна производиться не когда придется, а в нужный момент, и не должна понижать эффективность прикладного решения.

*Действия, логически объединенные в одну транзакцию (например, запись реквизитов объекта), нужно выполнять в одном серверном вызове. Это обеспечит согласованность данных объекта и избавит от лишних вызовов сервера.*

*При выборе картинки объекта, пока объект не записан пользователем, загруженные картинки должны быть во временном хранилище. Как только объект записывается, нужно переносить картинки из временного хранилища в реквизит и записывать синхронно с объектом (в одной транзакции).*

Что плохого, если записывать картинку в базу сразу после ее выбора? При этом будут произведены лишние вызовы сервера, ведь нет уверенности, что именно эта картинка и окажется в объекте в результате действий пользователя. Пользователь может вообще отказаться от записи объекта в базу данных. Также тут есть вопрос согласованности данных. Картинку записали, а объект – «не смогли». В результате данные могут быть несогласованными. То есть такие изменения должны выполняться в одной транзакции, за один серверный вызов.

Поэтому *не нужно записывать картинку в базу данных сразу при ее выборе, а делать это нужно непосредственно перед записью самого объекта в базу данных.*

Таким образом, из показанных вариантов решения задачи более эффективным будет
второй – за счет записи картинки объекта синхронно с самим объектом *в одной транзакции за один серверный вызов.*
